// 執行頻率需為 N 分鐘
variable: todayDate(0); todayDate = GetField("Date"); 
variable: todayTime(0); todayTime = GetField("Time");

input: BasePrice(0);	// 進場 BasePrice 
          
// 計算均線   
variable: ave2(0); 		ave2 = Average(Close,2);		// 2k 均線
variable: ave45(0); 	ave45 = Average(Close,45);		// 45k 均線
variable: ave270(0); 	ave270 = Average(Close,270);	// 270k 均線 (近月線)
variable: ave1200(0); 	ave1200 = Average(Close,1200);	// 1200k 均線 (近季線)
variable: aveDir2(0);	aveDir2 = Sign(ave2 - ave2[1]);			
variable: aveDir45(0);	aveDir45 = Sign(ave45 - ave45[1]);		
variable: aveDir270(0);	aveDir270 = Sign(ave270 - ave270[1]);	
variable: aveDir1200(0);aveDir1200 = Sign(ave1200 - ave1200[1]);

// 計算MACD
input: FastLength(12), SlowLength(26), MACDLength(9);	// 依照XQ軟體預設值
variable: difValue(0), macdValue(0), oscValue(0);
MACD(weightedclose(), FastLength, SlowLength, MACDLength, difValue, macdValue, oscValue);
variable: macdDir(0); macdDir = Sign(macdValue - macdValue[2]);		// MACD 方向
variable: difDir(0); difDir = Sign(difValue - difValue[2]);			// DIF 方向 (向下 => -1)
variable: macdUnder(0); macdUnder = Sign(difValue - macdValue);		// DIF 和 MACD 的上下位置

// 1. 累計委差-市場氣氛 (大 => 多; 小 => 空)
// 2. 成交筆差-多空力道 (大 => 多; 小 => 空)
value1 = GetQuote("累計委買") - GetQuote("累計委賣");
value2 = GetQuote("累賣成筆") - GetQuote("累買成筆");

// 盤中資訊
print("==============================================================");
print("當日漲幅:",GetQuote("當日漲幅%"));
print("日期時間:",DateToString(todayDate),TimeToString(todayTime));
print("AD45分數:",NumToStr(Summation(aveDir45,35),0));
print("市場氣氛:",NumToStr(value1, 0),"多空力道:",NumToStr(value2, 0));

///////////////////// 交易格局 ///////////////////// 

// 指數要高於 BasePrice 才啟動策略 (BasePrice 要手動設定)
// 1. 基礎單直接啟動; 加碼單則基礎單獲利 > 50點再進場
condition1 = close < BasePrice and BasePrice <> 0;
if condition1 then
begin
    print("指數要高於 BasePrice 才啟動策略 => 休息");
    return;
end;

// 策略執行條件: 季線下彎, 避免壓力過大追多單
// 1. 1200K斜率明顯向下: 51K之內 斜率下降超過(40/8000)
// 2. 指數離1200K不遠: (指數 * 1.06 > 1200K)
condition1 = (ave1200[51] - ave1200) > close * 0.0045;
condition2 = close * 1.06 > ave1200;
if condition1 and condition2 then
begin
    print("1200K下彎, 避免壓力過大追多單");
    return;
end;

// 策略執行條件: 
// 1. 15:00 to 18:00 休息
// 2. 13:00 to 13:45 休息
// 3. 04:00 to 05:00 休息
condition1 = todayTime >= 150000 and todayTime <= 180000;
condition2 = todayTime >= 130000 and todayTime <= 134500;
condition3 = todayTime >= 040000 and todayTime <= 050000;
if condition1 or condition2 or condition3 then
begin
    print("休息時間!!");
    return;
end;

// 策略執行條件: 
// 1. 過去18K漲幅沒超過20點 => 休息
condition1 = close - lowest(low,18) < 20;
if condition1 then
begin
    print("過去18K漲幅沒超過20點 => 休息");
    return;
end;

// 夜盤大漲超過 0.8% 不要追多單, 多單常常是緩漲的
condition1 = (todayTime >= 150000 and todayTime <= 235900) or (todayTime >= 000000 and todayTime <= 050000);
condition2 = GetQuote("當日漲幅%") > 0.8;
if condition1 and condition2 then
begin
    print("夜盤大漲超過 0.8% 不要追多單 => 休息");
    return;
end;

///////////////////// 交易策略 /////////////////////  

// 策略3: 指數大跌之後搶反彈
// 1. 1200K乖離超過550點 (550/8000) 
// 2. 指數離45K 90點內 (90/8000)
// 3. DIF向上 and 45K方向向上
// 4. 指數高於 ave45 6K
// 5. 近24K拉回小於60點 (60/8000) (創高後不能拉回太深)
// 6. 指數為正
condition31 = ave1200 > close * 1.06875;
condition32 = close < ave45 * 1.01125;
condition33 = difDir = 1 and aveDir45 = 1;
condition34 = close > ave45 and close[3] > ave45[3] and close[6] > ave45[6]; 
condition35 = highest(high, 24) < close * 1.0075;
condition36 = GetQuote("當日漲幅%") > 0;
print("策略3: ",DateToString(todayDate),TimeToString(todayTime), condition31, condition32, condition33, condition34, condition35, condition36);
if condition31 and condition32 and condition33 and condition34 and condition35 and condition36 then
begin
	ret = 1;
end;

// 策略2 (拉回季線,季線向上 寬鬆買點)
// 1. 1200K向上 (近季線)
// 2. 過去45K當中 aveDir90 分數高於13
// 3. DIF向上
// 4. 指數高於 ave90
// 5. 2K均線方向 = 1
// 6. 270K乖離要小於3% (240/8000)
// 7. 斜率 - 指數過去3天有低於1200K(35/8000)
condition21 = aveDir1200 = 1 and aveDir1200[3] = 1 and aveDir1200[6] = 1 and aveDir1200[9] = 1;
condition22 = Summation(aveDir45,35) > 13;
condition23 = difDir = 1;
condition24 = close > ave45;
condition25 = aveDir2 = 1;
condition26 = ave270 * 1.03 > close;
condition27 = ave1200 * 1.004375 > Lowest(low, 57);
print("策略2: ",DateToString(todayDate),TimeToString(todayTime), condition21, condition22, condition23, condition24, condition25, condition26, condition27);
if condition21 and condition22 and condition23 and condition24 and condition25 and condition26 and condition27 then
begin
	ret = 1;
end;

// 策略1
// 1. 過去35K當中 aveDir45 分數高於28
// 2. DIF向上
// 3. 指數高於 ave45
// 4. 2K均線方向 = 1
// 5. 270K(近月線)乖離要小於3% (240/8000)
condition11 = (Summation(aveDir45,35) > 19 or Summation(aveDir45,35) = 19);
condition12 = difDir = 1;
condition13 = close > ave45;
condition14 = aveDir2 = 1;
condition15 = ave270 * 1.03 > close;
print("策略1: ",DateToString(todayDate),TimeToString(todayTime), condition11, condition12, condition13, condition14, condition15 );
if condition11 and condition12 and condition13 and condition14 and condition15 then
begin
	ret = 1;
end;
