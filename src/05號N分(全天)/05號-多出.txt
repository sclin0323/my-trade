variable: todayDate(0); todayDate = GetField("Date");
variable: todayTime(0); todayTime = GetField("Time");  
       
input: costPrice(0);	// 我的成本
if costPrice = 0 then begin 
	print("請輸入目前成本!!");
	return;
end;  
   
// 計算均線  
variable: ave9(0); 		ave9 = Average(Close,9);				// 9k均線
variable: ave45(0); 	ave45 = Average(Close,45);				// 45k均線
variable: aveDir9(0);	aveDir9 = Sign(ave9 - ave9[1]);			// 9k均線方向
variable: aveDir45(0);	aveDir45 = Sign(ave45 - ave45[1]);		// 45k均線方向


// 計算MACD
input: FastLength(15), SlowLength(19), MACDLength(10);	// 依照XQ軟體預設值
variable: difValue(0), macdValue(0), oscValue(0);
MACD(weightedclose(), FastLength, SlowLength, MACDLength, difValue, macdValue, oscValue);
variable: macdDir(0); macdDir = Sign(macdValue - macdValue[2]);		// MACD 方向
variable: difDir(0); difDir = Sign(difValue - difValue[2]);			// DIF 方向 (向下 => -1)
variable: macdUnder(0); macdUnder = Sign(difValue - macdValue);		// DIF 和 MACD 的上下位置

// 1. 累計委差-市場氣氛 (大 => 多; 小 => 空)
// 2. 成交筆差-多空力道 (大 => 多; 小 => 空)
value1 = GetQuote("累計委買") - GetQuote("累計委賣");
value2 = GetQuote("累賣成筆") - GetQuote("累買成筆");

print("================================================");
print("時間",DateToString(todayDate),TimeToString(todayTime),"指數:",NumToStr(close, 0),"市場氣氛:",NumToStr(value1, 0),"多空力道:",NumToStr(value2, 0));

///////////////////// 交易格局 ///////////////////// 

// 策略執行條件-1
// 1. 如果 多空力道 > 1200 and 上漲0.3 表示多方力道很強 => 不要出 
// 2. 如果 大漲超過 0.8 表示多方力道很強 => 不要出 
condition1 = value2 > 1200 and GetQuote("當日漲幅%") > 0.3;
condition2 = GetQuote("當日漲幅%") > 0.8;
if condition1 or condition2 then
begin
    print("策略執行條件 不成立!!");
    return;
end;

///////////////////// 夜盤 - 交易策略 ///////////////////// 

// 時間: 20:00 to 23:55 or 00:00 to 05:00
// 嚴防夜盤出現超大跌走勢 先跑再說 (大盤緩漲急跌)
// 1. 跌幅超過0.2%
// 2. 獲利 < 15點
// 3. 過去192K內大跌超過175點
if (todayTime >= 200000 and todayTime <= 235500) or (todayTime >= 000000 and todayTime < 050000) then
begin
	condition331 = GetQuote("當日漲幅%") < -0.2;
	condition332 = close - costPrice < 15;
	condition333 = highest(close, 192) - close > 175;
	print("策略33: ", condition331, condition332, condition333);
	if condition331 and condition332 and condition333 then
	begin
		ret = 1; return;
	end;
end;

// 時間: 20:00 to 23:55 or 00:00 to 05:00
// 嚴防夜盤出現大跌走勢 先跑再說 (大盤緩漲急跌)
// 1. 跌幅超過0.35%
// 2. 獲利 < 15點
// 3. 過去192K內大跌超過125點
if (todayTime >= 200000 and todayTime <= 235500) or (todayTime >= 000000 and todayTime < 050000) then
begin
	condition321 = GetQuote("當日漲幅%") < -0.35;
	condition322 = close - costPrice < 15;
	condition323 = highest(close, 192) - close > 125;
	print("策略32: ", condition321, condition322, condition323);
	if condition321 and condition322 and condition323 then
	begin
		ret = 1; return;
	end;
end;


// 時間: 20:00 to 23:55 or 00:00 to 05:00
// 夜盤停損出場不要廢話
if (todayTime >= 200000 and todayTime <= 235500) or (todayTime >= 000000 and todayTime < 050000) then
begin
	condition311 = close - costPrice < -80;
	print("策略31: ", condition311);
	if condition311 then
	begin
		ret = 1; return;
	end;
end;

// 時間: 20:00 to 23:55 or 00:00 to 05:00 (通常已經大跌了)
// 夜盤由多逐漸走空出場
// 1. 指數低於45k均線
// 2. 78K內跌幅超過45
// 3. 大盤跌幅超過0.2
// 4. DIF向下
// 5. 45k向下超過32K(32/48)
if (todayTime >= 200000 and todayTime <= 235500) or (todayTime >= 000000 and todayTime < 050000) then
begin
	condition301 = close < ave45;
	condition302 = highest(high, 78) - close > 45;
	condition303 = GetQuote("當日漲幅%") < -0.2;
	condition304 = difDir = -1;
	condition305 = Summation(aveDir45,48) <= -16;
	print("策略30: ", condition301, condition302, condition303, condition304, condition305);
	if condition301 and condition302 and condition303 and condition304 and condition305 then
	begin
		ret = 1; return;
	end;
end;

///////////////////// 早盤 - 交易策略 ///////////////////// 

// 時間: 08:55 to 13:45 (停損)
// 停損出場不要廢話
// 1. 虧損超過80點
if todayTime >= 085500 and todayTime < 134500 then
begin
	condition71 = close - costPrice < -80;
	print("策略7: ", condition71);
	if condition71 then
	begin
		ret = 1; return;
	end;
end;

// 時間: 08:55 to 13:45 (停利)
// 獲利超過260點 and 215點停利出場
// 1. 過去3天內獲利超過260點 (近3天內獲利有超過200點)
// 2. 獲利小於215點(連續2k)
// 3. DIF向下
// 4. 9k均線方向向下
// 5. 漲幅要低於0.3以下
if todayTime >= 085500 and todayTime < 134500 then
begin
	condition61 = highest(high, 180) - costPrice > 260;
	condition62 = close - costPrice < 215 and (close[1] - costPrice < 215 or close[2] - costPrice < 215);
	condition63 = difDir = -1;
	condition64 = aveDir9 = -1;
	condition65 = GetQuote("當日漲幅%") < 0.3;
	print("策略6: ", condition61, condition62, condition63, condition64, condition65);
	if condition61 and condition62 and condition63 and condition64 and condition65 then
	begin
		ret = 1; return;
	end;
end;

// 時間: 08:55 to 13:45 (停利)
// 獲利超過200點 and 155點停利出場
// 1. 過去3天內獲利超過200點 (近3天內獲利有超過200點)
// 2. 獲利小於155點(連續2k)
// 3. DIF向下
// 4. 9k均線方向向下
// 5. 漲幅要低於0.3以下
if todayTime >= 085500 and todayTime < 134500 then
begin
	condition51 = highest(high, 180) - costPrice > 200;
	condition52 = close - costPrice < 155 and (close[1] - costPrice < 155 or close[2] - costPrice < 155);
	condition53 = difDir = -1;
	condition54 = aveDir9 = -1;
	condition55 = GetQuote("當日漲幅%") < 0.3;
	print("策略5: ", condition51, condition52, condition53, condition54, condition55);
	if condition51 and condition52 and condition53 and condition54 and condition55 then
	begin
		ret = 1; return;
	end;
end;

// 時間: 08:55 to 13:45 (停利)
// 獲利超過140點 and 100點停利出場
// 1. 過去3天內獲利超過140點 (近3天內獲利有超過140點)
// 2. 獲利小於100點
// 3. DIF向下
// 4. 9k均線方向向下
// 5. 漲幅要低於0.3以下
if todayTime >= 085500 and todayTime < 134500 then
begin
	condition41 = highest(high, 180) - costPrice > 140;
	condition42 = close - costPrice < 100 and (close[1] - costPrice < 100 or close[2] - costPrice < 100);
	condition43 = difDir = -1;
	condition44 = aveDir9 = -1;
	condition45 = GetQuote("當日漲幅%") < 0.3;
	print("策略4: ", condition41, condition42, condition43, condition44, condition45);
	if condition41 and condition42 and condition43 and condition44 and condition45 then
	begin
		ret = 1; return;
	end;
end;

// 時間: 08:55 to 13:45 (停利)
// 獲利超過95點 and 65點停利出場
// 1. 過去3天內獲利超過95點 (近3天內獲利有超過95點)
// 2. 獲利小於65點
// 3. DIF向下
// 4. 9k均線方向向下 or 一小時大跌75點
if todayTime >= 085500 and todayTime < 134500 then
begin
	condition31 = highest(high, 180) - costPrice > 95;
	condition32 = close - costPrice < 65 and (close[1] - costPrice < 65 or close[2] - costPrice < 65);
	condition33 = difDir = -1;
	condition34 = aveDir9 = -1 or (highest(high,5) - close > 75);
	print("策略3: ", condition31, condition32, condition33, condition34);
	if condition31 and condition32 and condition33 and condition34 then
	begin
		ret = 1; return;
	end;
end;

// 時間: 08:40 to 13:45 (小賺小賠)
// 小獲利漲勢不足出場, 預防一天漲一天跌 (出場後立即啟動短線策略)
// 1. 獲利25點之內
// 2. 9k均線向下 or 一小時大跌75點
// 3. DIF向下
// 4. 近36k跌幅超過25點
if todayTime >= 084000 and todayTime < 134500 then
begin
	condition21 = close - costPrice < 25;
	condition22 = aveDir9 = -1 or (highest(high,5) - close > 75);
	condition23 = difDir = -1;
	condition24 = highest(close, 36) - close > 25;
	print("策略2: ", condition21, condition22, condition23, condition24);
	if condition21 and condition22 and condition23 and condition24 then
	begin
		ret = 1; return;
	end;
end;

// 時間: 08:55 to 13:45 (停利or小賠)
// 大盤由多逐漸走空出場 (適合停利使用)
// 1. 如果紅盤 => 過去21k線內 DIF值有超過15k(包含)向下(Summation 小於-11) Or 如果黑盤 => 過去21k線內 DIF值有超過15k(包含)向下(Summation 小於-9)
// 2. DIF向下
// 3. 指數低於9k均線
// 4. 指數比過去78K線最高 跌點超過40點
// 5. 9k均線方向向下
if todayTime >= 085500 and todayTime < 134500 then
begin
	condition11 = (GetQuote("當日漲幅%") > 0 and ((Summation(difDir,21) < -11 or Summation(difDir,21) = -11))) or (GetQuote("當日漲幅%") < 0 and ((Summation(difDir,21) < -9 or Summation(difDir,21) = -9)));
	condition12 = difDir = -1;
	condition13 = close < ave9;
	condition14 = highest(high, 78) - close > 40;
	condition15 = aveDir9 = -1;
	print("策略1: ", condition11, condition12, condition13, condition14, condition15);
	if condition11 and condition12 and condition13 and condition14 and condition15 then
	begin
		ret = 1; return;
	end;
end;
