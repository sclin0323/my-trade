// 執行頻率需為 N 分鐘
variable: todayDate(0); todayDate = GetField("Date"); 
variable: todayTime(0); todayTime = GetField("Time");
          
input: BasePrice(0);	// 進場 BasePrice 
		  
// 計算均線 
variable: ave2(0); 		ave2 = Average(Close,2);		// 2k 均線
variable: ave45(0); 	ave45 = Average(Close,45);		// 45k 均線
variable: ave270(0); 	ave270 = Average(Close,270);	// 270k 均線 (近月線)
variable: ave1200(0); 	ave1200 = Average(Close,1200);	// 1200k 均線 (近季線)
variable: aveDir2(0);	aveDir2 = Sign(ave2 - ave2[1]);			
variable: aveDir45(0);	aveDir45 = Sign(ave45 - ave45[1]);		
variable: aveDir270(0);	aveDir270 = Sign(ave270 - ave270[1]);	
variable: aveDir1200(0);aveDir1200 = Sign(ave1200 - ave1200[1]);

// 計算MACD
input: FastLength(12), SlowLength(26), MACDLength(9);	// 依照XQ軟體預設值
variable: difValue(0), macdValue(0), oscValue(0);
MACD(weightedclose(), FastLength, SlowLength, MACDLength, difValue, macdValue, oscValue);
variable: macdDir(0); macdDir = Sign(macdValue - macdValue[2]);		// MACD 方向
variable: difDir(0); difDir = Sign(difValue - difValue[2]);			// DIF 方向 (向下 => -1)
variable: macdUnder(0); macdUnder = Sign(difValue - macdValue);		// DIF 和 MACD 的上下位置


// 1. 累計委差-市場氣氛 (大 => 多; 小 => 空)
// 2. 成交筆差-多空力道 (大 => 多; 小 => 空)
value1 = GetQuote("累計委買") - GetQuote("累計委賣");
value2 = GetQuote("累賣成筆") - GetQuote("累買成筆");

// 盤中資訊
print("==============================================================");
print("當日漲幅:",GetQuote("當日漲幅%"));
print("日期時間:",DateToString(todayDate),TimeToString(todayTime));
print("AD45分數:",NumToStr(Summation(aveDir45,35),0));
print("市場氣氛:",NumToStr(value1, 0),"多空力道:",NumToStr(value2, 0));

///////////////////// 交易格局 ///////////////////// 

// 指數要高於 BasePrice 才啟動策略 (BasePrice 要手動設定)
// 1. 基礎單直接啟動; 加碼單則基礎單獲利 > 50點再進場
condition1 = close > BasePrice and BasePrice <> 0;
if condition1 then
begin
    print("指數要低於 BasePrice 才啟動策略 => 休息");
    return;
end;


// 策略執行條件-1: 
// 1. 15:00 to 18:00 休息
// 2. 13:00 to 13:45 休息
// 3. 04:00 to 05:00 休息
condition1 = todayTime >= 150000 and todayTime <= 180000;
condition2 = todayTime >= 130000 and todayTime <= 134500;
condition3 = todayTime >= 040000 and todayTime <= 050000;
if condition1 or condition2 or condition3 then
begin
    print("休息時間!!");
    return;
end;

// 策略執行條件-1: 
// 1. 過去18K跌幅沒超過20點 => 休息
condition1 = highest(high,18) - close < 20;
if condition1 then
begin
    print("過去18K跌幅沒超過20點 => 休息!!");
    return;
end;

///////////////////// 交易策略 /////////////////////  

// 策略六: 大跌之後反彈無力再繼續空
// 1. 過去35K當中 aveDir45 分數低於0
// 2. 指數乖離1200K超過 550 點 (大跌)
// 3. DIF向下 & 指數低於 ave45 & 2小時均線方向 = -1
// 4. 54K大跌超過90點 (90/8000)
// 5. 指數離ave45 在 90點之內
// 6. 過去36k中上漲小於110 (110/8000)  (底部上漲不能太多)
// 7. 指數平盤下
condition61 = (Summation(aveDir45,35) < 0 or Summation(aveDir45,35) = 0);
condition62 = ave1200 > close * 1.06875;
condition63 = difValue < 0 and close < ave45 and aveDir2 = -1;
condition64 = highest(high, 54) > close * 1.01125;
condition65 = ave45 < close * 1.01125;
condition66 = close < lowest(low, 36) * 1.01375;
condition67 = GetQuote("當日漲幅%") < 0;
print("空-策略6: ",DateToString(todayDate),TimeToString(todayTime), condition61, condition62, condition63, condition64, condition65, condition66, condition67);
if condition61 and condition62 and condition63 and condition64 and condition65 and condition66 and condition67 then
begin
	ret = 1;
end;

// 策略五 (策略一改良版: 急跌瞬間方空, 因應緩漲急跌盤勢特性)
// 1. 過去35K當中 aveDir45 分數低於5
// 2. DIF向下9k and DIF為負
// 3. 指數低於 ave45
// 4. 2K均線方向 = -1 (2k)
// 5. 270K乖離要小於3% (240/8000)
// 6. 54K大跌超過1.25% (100/8000)
// 7. 1200K乖離要小於5% (400/8000)
// 8. 指數平盤下
condition51 = (Summation(aveDir45,35) < 5 or Summation(aveDir45,35) = 5);
condition52 = difDir = -1 and difDir[3] = -1 and difDir[9] = -1 and difValue < 0;
condition53 = close < ave45;
condition54 = aveDir2 = -1 and aveDir2[1] = -1;
condition55 = ave270 * 0.97 < close;
condition56 = highest(high, 54) * 0.9875 > close;
condition57 = ave1200 < close * 1.05;
condition58 = GetQuote("當日漲幅%") < 0;
print("空-策略5: ",DateToString(todayDate),TimeToString(todayTime), condition51, condition52, condition53, condition54, condition55, condition56, condition57, condition58);
if condition51 and condition52 and condition53 and condition54 and condition55 and condition56 and condition57 and condition58 then
begin
	ret = 1;
end;

// 策略四 (漲幅過高, 空單提前進場)
// 1. 過去35K當中 aveDir45 分數低於-7
// 2. DIF向下 連續9
// 3. 指數低於 ave45 * 1.00375 (30/8000)
// 4. 2小時均線方向 = -1
// 5. 270小時均線離指數乖離要超過1.375%(漲幅超漲 300HR乖離過大) (110/8000)
// 6. 過去54K 下跌要超過0.625% (50/8000)
// 7. 指數平盤下
condition41 = (Summation(aveDir45,35) < -7 or Summation(aveDir45,35) = -7);
condition42 = difDir = -1 and difDir[3] = -1 and difDir[9] = -1;
condition43 = close < ave45 * 1.004375;
condition44 = aveDir2 = -1;
condition45 = ave270 * 1.01375 < close;
condition46 = highest(high, 54) * 0.99375 > close;
condition47 = GetQuote("當日漲幅%") < 0;
print("空-策略4: ",DateToString(todayDate),TimeToString(todayTime), condition41, condition42, condition43, condition44, condition45, condition46, condition47);
if condition41 and condition42 and condition43 and condition44 and condition45 and condition46 and condition47 then
begin
	ret = 1;
end;

// 策略三 (漲回季線, 季線向下 寬鬆賣點)
// 1. 1200K 向下 and 指數過去3天有高於1200K (35/8000)(斜率要出來)
// 2. 過去35K當中 aveDir45 分數低於-9
// 3. DIF向下
// 4. 指數低於 ave45
// 5. 2小時均線方向 = -1
// 6. 270K(近月線)乖離要小於3%
// 7. 1200K(近季線)向下
// 8. 指數平盤下
condition31 = aveDir1200 = -1 and (highest(high, 57) > ave1200 * 0.995625);
condition32 = (Summation(aveDir45,35) < -9 or Summation(aveDir45,35) = -9);
condition33 = difDir = -1;
condition34 = close < ave45;
condition35 = aveDir2 = -1;
condition36 = ave270 * 0.97 < close;
condition37 = aveDir1200 = -1;
condition38 = GetQuote("當日漲幅%") < 0;
print("空-策略3: ",DateToString(todayDate),TimeToString(todayTime), condition31, condition32, condition33, condition34, condition35, condition36, condition37, condition38);
if condition31 and condition32 and condition33 and condition34 and condition35 and condition36 and condition37 and condition38 then
begin
	ret = 1;
end;

// 策略二 (出現大跌, 提早放空)
// 1. 過去35K當中 aveDir45 分數低於-13
// 2. 過去18k當中大跌超過1%
// 3. DIF向下
// 4. 指數低於 ave45
// 5. 2K 均線方向 = -1
// 6. 270K 乖離要小於3%
condition21 = (Summation(aveDir45,35) < -13 or Summation(aveDir45,35) = -13);
condition22 = highest(high, 18) * 0.99 > close;
condition23 = difDir = -1;
condition24 = close < ave45;
condition25 = aveDir2 = -1;
condition26 = ave270 * 0.97 < close;
print("空-策略2: ",DateToString(todayDate),TimeToString(todayTime), condition21, condition22, condition23, condition24, condition25, condition26 );
if condition21 and condition22 and condition23 and condition24 and condition25 and condition26 then
begin
	ret = 1;
end;

// 策略一
// 1. 過去35K當中 aveDir45 分數低於-28
// 2. DIF向下
// 3. 指數低於 ave45
// 4. 2K均線方向 = -1
// 5. 270K乖離要小於3%
condition11 = (Summation(aveDir45,35) < -19 or Summation(aveDir45,35) = -19);
condition12 = difDir = -1;
condition13 = close < ave45;
condition14 = aveDir2 = -1;
condition15 = ave270 * 0.97 < close;
print("空-策略1: ",DateToString(todayDate),TimeToString(todayTime), condition11, condition12, condition13, condition14, condition15);
if condition11 and condition12 and condition13 and condition14 and condition15 then
begin
	ret = 1;
end;

