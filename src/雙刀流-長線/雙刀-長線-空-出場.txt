// 執行頻率需為 60 分鐘
if barinterval <> 60 then RaiseRunTimeError("請設定頻率為60分鐘");
variable: todayDate(0); todayDate = GetField("Date"); 
variable: todayTime(0); todayTime = GetField("Time");
 
// 計算均線
variable: ave18(0); 	ave18 = Average(Close,18);			// 18小時均線	
variable: aveDir18(0);	aveDir18 = Sign(ave18 - ave18[1]);	// 18小時均線方向 
variable: ave300(0); 	ave300 = Average(Close,300);		// 300小時均線(季線)
variable: aveDir300(0);	aveDir300 = Sign(ave300 - ave300[1]);	// 300小時均線方向(季線)

// 計算MACD
input: FastLength(12), SlowLength(26), MACDLength(9);	// 依照XQ軟體預設值
variable: difValue(0), macdValue(0), oscValue(0);
MACD(weightedclose(), FastLength, SlowLength, MACDLength, difValue, macdValue, oscValue);
variable: macdDir(0); macdDir = Sign(macdValue - macdValue[2]);		// MACD 方向
variable: difDir(0); difDir = Sign(difValue - difValue[2]);			// DIF 方向 (向下 => -1)
variable: macdUnder(0); macdUnder = Sign(difValue - macdValue);		// DIF 和 MACD 的上下位置

print("====== 雙刀-長線-空 出場 ======",DateToString(todayDate),TimeToString(todayTime));

// 1. 季線向上(嚴格出場)
// 2. DIF向上
// 3. 5小時內最低點 - 季線 < 30點(指數來到上升季線)
// 4. 5小時內反彈上漲超過40點
condition11 = aveDir300 = 1 or aveDir300[1] = 1;
condition12 = difDir = 1;
condition13 = Lowest(low, 5) - ave300 < 30;
condition14 = close - Lowest(low, 5) > 40;

print("季線向上出場: ", condition11, condition12, condition13, condition14);
if condition11 and condition12 and condition13 and condition14 then
begin
	ret = 1;
end;

// 0. 季線向下或向上都適用(寬鬆出場)
// 1. DIF向上
// 2. 指數高於 ave18 2K
condition61 = aveDir300 = -1 or aveDir300 = 1;
condition62 = difDir = 1;
condition63 = close > ave18 and close[1] > ave18[1];

print("季線向下或上出場: ", condition61, condition62, condition63);
if condition61 and condition62 and condition63 then
begin
	ret = 1;
end;
