if barinterval <> 1 then RaiseRunTimeError("請設定頻率為1分鐘");
variable: todayDate(0); todayDate = GetField("Date");
variable: todayTime(0); todayTime = GetField("Time"); 
   
// 我的成本  
input: cost(0);	
if cost = 0 then begin 
	print("請輸入目前成本!!");
	return;
end;

// 計算均線  
variable: ave8(0); 		ave8 = Average(Close,8);		// 8分鐘均線
variable: ave17(0); 	ave17 = Average(Close,17);		// 17分鐘均線
variable: ave34(0); 	ave34 = Average(Close,34);		// 34分鐘均線
variable: ave68(0); 	ave68 = Average(Close,68);		// 68分鐘均線
variable: ave130(0); 	ave130 = Average(Close,130);	// 130分鐘均線
variable: aveDir8(0);	aveDir8 = Sign(ave8 - ave8[1]);		// 8分鐘均線方向
variable: aveDir17(0);	aveDir17 = Sign(ave17 - ave17[1]);	// 17分鐘均線方向
variable: aveDir34(0);	aveDir34 = Sign(ave34 - ave34[1]);	// 34分鐘均線方向
variable: aveDir68(0);	aveDir68 = Sign(ave68 - ave68[1]);	// 68分鐘均線方向
variable: aveDir130(0);	aveDir130 = Sign(ave130 - ave130[1]);	// 130分鐘均線方向

// 計算MACD
input: FastLength(18), SlowLength(32), MACDLength(12);	// 依照XQ軟體預設值
variable: difValue(0), macdValue(0), oscValue(0);
MACD(weightedclose(), FastLength, SlowLength, MACDLength, difValue, macdValue, oscValue);
variable: macdDir(0); macdDir = Sign(macdValue - macdValue[2]);		// MACD 方向
variable: difDir(0); difDir = Sign(difValue - difValue[2]);			// DIF 方向 (向下 => -1)
variable: macdUnder(0); macdUnder = Sign(difValue - macdValue);		// DIF 和 MACD 的上下位置

print("= 夜短多-出 ",DateToString(todayDate)," ",TimeToString(todayTime)," ",NumToStr(close, 0)," =");


// 策略1: 夜短多-出 停損停利出場
// 1. 130分線連續2分鐘向下
// 2. 指數低於130分線 連續2分鐘
// 3. 獲利低於15點 or 高檔拉回25點
// 4. DIF向下 
condition11 = aveDir130 = -1 and aveDir130[1] = -1;
condition12 = close < ave130 and close < ave130[1];
condition13 = (close - cost < 15) or (highest(close, 45) - close > 25);
condition14 = difDir = -1;
	
print("夜短多-出-策略1: ", condition11, condition12, condition13, condition14);
if condition11 and condition12 and condition13 and condition14 then
begin
	ret = 1;
end;

// 策略2: 夜短空-出 收盤出場
// 時間: 4:55 to 4:59
// 1. 獲利如果小於30點, 直接獲利出場
if (todayTime >= 045500 and todayTime < 045900) then
begin
	condition21 = (close - cost < 30);
	
	print("夜短多-出-策略2: ", condition21);
	if condition21 then
	begin
		ret = 1;
	end;
end;
