// 執行頻率需為 1 分鐘
if barfreq <> "Min" or barinterval <> 1 then RaiseRunTimeError("請設定頻率為1分鐘");
variable: todayDate(0); todayDate = GetField("Date"); 
variable: todayTime(0); todayTime = GetField("Time");
            
// 計算均線  
variable: ave8(0);      ave8 = Average(Close,8);    // 8分鐘均線
variable: ave17(0);     ave17 = Average(Close,17);  // 17分鐘均線
variable: ave34(0);     ave34 = Average(Close,34);  // 34分鐘均線
variable: ave68(0);     ave68 = Average(Close,68);  // 68分鐘均線
variable: ave130(0);    ave130 = Average(Close,130);    		// 130分鐘均線
variable: aveDir8(0);   aveDir8 = Sign(ave8 - ave8[1]);     	// 8分鐘均線方向
variable: aveDir17(0);  aveDir17 = Sign(ave17 - ave17[1]);  	// 17分鐘均線方向
variable: aveDir34(0);  aveDir34 = Sign(ave34 - ave34[1]);  	// 34分鐘均線方向
variable: aveDir68(0);  aveDir68 = Sign(ave68 - ave68[1]);  	// 68分鐘均線方向
variable: aveDir130(0); aveDir130 = Sign(ave130 - ave130[1]);   // 130分鐘均線方向
  
// 計算MACD
input: FastLength(18), SlowLength(32), MACDLength(12);	// 依照XQ軟體預設值
variable: difValue(0), macdValue(0), oscValue(0);
MACD(weightedclose(), FastLength, SlowLength, MACDLength, difValue, macdValue, oscValue);
variable: macdDir(0); macdDir = Sign(macdValue - macdValue[2]);     // MACD 方向
variable: difDir(0); difDir = Sign(difValue - difValue[2]);         // DIF 方向 (向下 => -1)
variable: macdUnder(0); macdUnder = Sign(difValue - macdValue);     // DIF 和 MACD 的上下位置
 
// 大盤多空判斷
value1 = GetQuote("累計委買") - GetQuote("累計委賣");   // 委買口數差(成交) 作多: 數值越大越好
value2 = GetQuote("累買成筆") - GetQuote("累賣成筆");   // 成交買賣差(成交) 作多: 數值越小越好
print("= 夜短多 ",DateToString(todayDate),TimeToString(todayTime),"|","指數:",NumToStr(close, 0),"DIF值:",NumToStr(difValue,1),"DIF方向:",NumToStr(difDir,0),"|",NumToStr(value1, 2),NumToStr(value2, 0)," =");


// 策略: 130分線做多趨勢, 拉回找買點
// 1. 130分線 30分鐘陡升超過6點 or 60分鐘陡升超過10點
// 2. 指數離130分線12點之內
// 3. DIF方向2分鐘向上
// 4. value2 < 100
condition11 = (ave130 - ave130[60] > 10) or (ave130 - ave130[30] > 6);
condition12 = close - ave130 < 12 and close - ave130 > -12;
condition13 = difDir = 1 and difDir[1] = 1; 
condition14 = value2 < -100;
	 
print("夜短多-策略1: ", condition11, condition12, condition13, condition14);
if condition11 and condition12 and condition13 and condition14 then
begin
	ret = 1;
end;