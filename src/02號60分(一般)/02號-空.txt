// 執行頻率需為 60 分鐘
if barinterval <> 60 then RaiseRunTimeError("請設定頻率為60分鐘");
variable: todayDate(0); todayDate = GetField("Date"); 
variable: todayTime(0); todayTime = GetField("Time");
          
// 計算均線 
variable: ave2(0); 		ave2 = Average(Close,2);			// 2小時均線
variable: ave18(0); 	ave18 = Average(Close,18);			// 18小時均線	
variable: ave90(0); 	ave90 = Average(Close,90);			// 90小時均線
variable: ave300(0); 	ave300 = Average(Close,300);		// 300小時均線(季線)
variable: aveYear(0); 	aveYear = Average(Close,1200);		// 年線
variable: aveDir2(0);	aveDir2 = Sign(ave2 - ave2[1]);		// 2小時均線方向
variable: aveDir18(0);	aveDir18 = Sign(ave18 - ave18[1]);	// 18小時均線方向 
variable: aveDir300(0);	aveDir300 = Sign(ave300 - ave300[1]);	// 300小時均線方向(季線)
variable: aveDirYear(0);aveDirYear = Sign(aveYear - aveYear[1]);	// 年線方向

// 計算MACD
input: FastLength(12), SlowLength(26), MACDLength(9);	// 依照XQ軟體預設值
variable: difValue(0), macdValue(0), oscValue(0);
MACD(weightedclose(), FastLength, SlowLength, MACDLength, difValue, macdValue, oscValue);
variable: macdDir(0); macdDir = Sign(macdValue - macdValue[2]);		// MACD 方向
variable: difDir(0); difDir = Sign(difValue - difValue[2]);			// DIF 方向 (向下 => -1)
variable: macdUnder(0); macdUnder = Sign(difValue - macdValue);		// DIF 和 MACD 的上下位置


// 盤中資訊
print("==============================================================");
print("aveDir18 分數(需小於-7):",NumToStr(Summation(aveDir18,15),0));
print("DIF方向(需為負向):",difDir);
print("指數(要低於ave18):",NumToStr(close, 0)," ave18:",NumToStr(ave18,0));
print("2小時均線方向(需為負向):",aveDir2);
print("月線(90小時)線離指數差(需小於240):",ave90-close);
print("季線(300小時)方向:",aveDir300);

// 策略五 (策略一改良版: 急跌瞬間方空, 因應緩漲急跌盤勢特性)
// 1. 過去15K當中 aveDir18 有一些向下
// 2. DIF向下5k and DIF為負
// 3. 指數低於 ave18
// 4. 2小時均線方向 = -1
// 5. 90小時均線離指數乖離要小於240點
// 6. 18K大跌超過120點
condition11 = (Summation(aveDir18,15) < 7 or Summation(aveDir18,15) = 7);
condition12 = difDir = -1 and difDir[1] = -1 and difDir[3] = -1 and difDir[5] = -1 and difValue < 0;
condition13 = close < ave18;
condition14 = aveDir2 = -1;
condition15 = ave90 - close < 240;
condition16 = highest(high, 18) - close > 120;
print("空-策略5: ",DateToString(todayDate),TimeToString(todayTime), condition11, condition12, condition13, condition14, condition15, condition16);
if condition11 and condition12 and condition13 and condition14 and condition15 and condition16 then
begin
	ret = 1;
end;

// 策略四 (漲幅過高, 空單提前進場)
// 1. 過去15K當中 aveDir18 有超過7K向下
// 2. DIF向下 連續5
// 3. 指數低於 (ave18 + 30)
// 4. 2小時均線方向 = -1
// 5. 90小時均線離指數乖離要小於-110點(漲幅超漲 月線乖離過大)
// 6. DIF要小於13
// 7. 過去18K 下跌要超過50點
condition41 = (Summation(aveDir18,15) < 1 or Summation(aveDir18,15) = 1);
condition42 = difDir = -1 and difDir[1] = -1 and difDir[4] = -1;
condition43 = close < ave18 + 30;
condition44 = aveDir2 = -1;
condition45 = ave90 - close < -110;
condition46 = difValue < 15;
condition47 = highest(high, 18) - close > 50;
print("空-策略4: ",DateToString(todayDate),TimeToString(todayTime), condition41, condition42, condition43, condition44, condition45, condition46, condition47);
if condition41 and condition42 and condition43 and condition44 and condition45 and condition46 and condition47 then
begin
	ret = 1;
end;


// 策略三 (漲回季線, 季線向下 寬鬆賣點)
// 1. 季線向下(斜率要出來) and 指數過去3天有高於季線-35點
// 2. 過去15K當中 aveDir18 有超過9K向下
// 3. DIF向下
// 4. 指數低於 ave18
// 5. 2小時均線方向 = -1
// 6. 90小時乖離要小於240
// 7. 年線向下
condition31 = aveDir300 = -1 and (highest(high, 15) > ave300 - 35);
condition32 = (Summation(aveDir18,15) < -3 or Summation(aveDir18,15) = -3);
condition33 = difDir = -1;
condition34 = close < ave18;
condition35 = aveDir2 = -1;
condition36 = ave90 - close < 240;
condition37 = aveDirYear = -1;
print("空-策略3: ",DateToString(todayDate),TimeToString(todayTime), condition31, condition32, condition33, condition34, condition35, condition36, condition37);
if condition31 and condition32 and condition33 and condition34 and condition35 and condition36 and condition37 then
begin
	ret = 1;
end;

// 策略二 (出現大跌, 提早放空)
// 1. 過去15K當中 aveDir18 有超過10K向下
// 2. 過去5k當中大跌超過100點
// 3. DIF向下
// 4. 指數低於 ave18
// 5. 2小時均線方向 = -1
// 6. 90小時均線離指數乖離要小於240點
condition21 = (Summation(aveDir18,15) < -5 or Summation(aveDir18,15) = -5);
condition22 = highest(high, 6) - close > 100;
condition23 = difDir = -1;
condition24 = close < ave18;
condition25 = aveDir2 = -1;
condition26 = ave90 - close < 240;
print("空-策略2: ",DateToString(todayDate),TimeToString(todayTime), condition21, condition22, condition23, condition24, condition25, condition26 );
if condition21 and condition22 and condition23 and condition24 and condition25 and condition26 then
begin
	ret = 1;
end;

// 策略一
// 1. 過去15K當中 aveDir18 有超過11K向下
// 2. DIF向下
// 3. 指數低於 ave18
// 4. 2小時均線方向 = -1
// 5. 90小時均線離指數乖離要小於240點
condition11 = (Summation(aveDir18,15) < -7 or Summation(aveDir18,15) = -7);
condition12 = difDir = -1;
condition13 = close < ave18;
condition14 = aveDir2 = -1;
condition15 = ave90 - close < 240;
print("空-策略1: ",DateToString(todayDate),TimeToString(todayTime), condition11, condition12, condition13, condition14, condition15);
if condition11 and condition12 and condition13 and condition14 and condition15 then
begin
	ret = 1;
end;



