if barinterval <> 15 then RaiseRunTimeError("請設定頻率為15分鐘");
variable: todayDate(0); todayDate = GetField("Date");
variable: todayTime(0); todayTime = GetField("Time"); 
  
input: costPrice(0);	// 我的成本
if costPrice = 0 then begin 
	print("請輸入目前成本!!");
	return;
end;  
   
// 計算均線
variable: ave72(0); 	ave72 = Average(Close,72);			// 18小時K (3~4天)
variable: ave400(0); 	ave400 = Average(Close,400);		// 月線
variable: ave1200(0); 	ave1200 = Average(Close,1200);		// 季線
variable: aveDir72(0);	aveDir72 = Sign(ave72 - ave72[1]);		// 18小時均線方向
variable: aveDir400(0);	aveDir400 = Sign(ave400 - ave400[1]);	// 月線方向
variable: aveDir1200(0);aveDir1200 = Sign(ave1200 - ave1200[1]);// 季線方向

// 計算MACD
input: FastLength(15), SlowLength(19), MACDLength(10);
variable: difValue(0), macdValue(0), oscValue(0);
MACD(weightedclose(), FastLength, SlowLength, MACDLength, difValue, macdValue, oscValue);
variable: macdDir(0); macdDir = Sign(macdValue - macdValue[2]);		// MACD 方向
variable: difDir(0); difDir = Sign(difValue - difValue[2]);			// DIF 方向 (向下 => -1)
variable: macdUnder(0); macdUnder = Sign(difValue - macdValue);		// DIF 和 MACD 的上下位置

print("2號-多出:",DateToString(todayDate),TimeToString(todayTime),NumToStr(close, 0));

///////////////////////////////////////////////////////////////////////////////////////////////

// 策略6: 獲利超過290點停利出場
// 1. 現在獲利小於250點
// 2. DIF向下
// 3. 過去出現290點獲利 and 回跌超過40點
// 4. 當日漲幅% < 0.2;
condition61 = close - costPrice < 250;
condition62 = difDir = -1;
condition63 = highest(close, 36) - costPrice > 290 and highest(close, 36) - close > 40;
condition64 = GetQuote("當日漲幅%") < 0.2;
print("策略6:", condition61, condition62, condition63, condition64);
if condition61 and condition62 and condition63 and condition64 then
begin
	ret = 1;
end;

// 策略5: 獲利超過300點停利出場
// 1. 現在獲利小於310點
// 2. DIF向下
// 3. 過去出現350點獲利 and 回跌超過45點
// 4. 當日漲幅% < -0.2;
condition51 = close - costPrice < 310;
condition52 = difDir = -1;
condition53 = highest(close, 36) - costPrice > 350 and highest(close, 36) - close > 45;
condition54 = GetQuote("當日漲幅%") < -0.2;
print("策略5:", condition51, condition52, condition53, condition54);
if condition51 and condition52 and condition53 and condition54 then
begin
	ret = 1;
end;


// 策略4: 月線季線都向下, 長多獲利不大直接先出場
// 1. 獲利小於35點
// 2. 月線, 季線方向都向下
// 3. DIF向下
// 4. 當日漲幅% < -0.3;
condition41 = close - costPrice < 35;
condition42 = aveDir400 = -1 and aveDir1200 = -1;
condition43 = difDir = -1;
condition44 = GetQuote("當日漲幅%") < -0.3;
print("策略4:", condition41, condition42, condition43, condition44);
if condition41 and condition42 and condition43 and condition44 then
begin
	ret = 1;
end;


// 策略3: 沒有獲利,長多提前出場
// 1. 沒有獲利
// 2. 72K線趨勢向下 or 虧損超過55點
// 3. DIF向下
// 4. 指數低於72k線K
condition31 = costPrice - close > 0;
condition32 = aveDir72 = -1 or (costPrice - close > 55);
condition33 = difDir = -1;
condition34 = close < ave72;
print("策略3:", condition31, condition32, condition33, condition34);
if condition31 and condition32 and condition33 and condition34 then
begin
	ret = 1;
end;


// 策略2: 月線空頭提前出場
// 1. 月線連續向下
// 2. DIF 3向下
// 3. 指數低於 18小時K
// 4. 指數低於月線
// 5. 大盤漲幅要小於0.3%
condition21 = aveDir400 = -1 and aveDir400[1] = -1 and aveDir400[2] = -1 and aveDir400[3] = -1 and aveDir400[4] = -1 and aveDir400[5] = -1;
condition22 = difDir = -1 and difDir[1] = -1 and difDir[2] = -1;
condition23 = close < ave72;
condition24 = close < ave400;
condition25 = GetQuote("當日漲幅%") < 0.3;
print("策略2:", condition21, condition22, condition23, condition24, condition25);
if condition21 and condition22 and condition23 and condition24 and condition25 then
begin
	ret = 1;
end;

// 策略1: 72k線DIF向下
// 1. 72K線趨勢向下
// 2. DIF向下連續3k向下
// 3. 指數低於72k線K 3K
condition11 = (aveDir72 = -1 and aveDir72[1] = -1 and aveDir72[2] = -1) or (aveDir72 = -1 and aveDir72[1] = -1 and aveDir72[2] = 1 and aveDir72[3] = -1);
condition12 = (difDir = -1 and difDir[1] = -1 and difDir[2] = -1) or (difDir = -1 and difDir[1] = -1 and difDir[2] = 1 and difDir[3] = -1);
condition13 = (close < ave72 and close[1] < ave72[1] and close[2] < ave72[2]) or (close < ave72 and close[1] < ave72[1] and close[2] > ave72[2] and close[3] < ave72[3]);
print("策略1:", condition11, condition12, condition13);
if condition11 and condition12 and condition13 then
begin
	ret = 1;
end;


