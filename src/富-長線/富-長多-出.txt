if barinterval <> 15 then RaiseRunTimeError("請設定頻率為15分鐘");
variable: todayDate(0); todayDate = GetField("Date");
variable: todayTime(0); todayTime = GetField("Time"); 
 
// 計算均線
variable: ave72(0); 	ave72 = Average(Close,72);			// 18小時K (3~4天)
variable: ave400(0); 	ave400 = Average(Close,400);		// 月線
variable: ave1200(0); 	ave1200 = Average(Close,1200);		// 季線
variable: aveDir72(0);	aveDir72 = Sign(ave72 - ave72[1]);		// 18小時均線方向
variable: aveDir400(0);	aveDir400 = Sign(ave400 - ave400[1]);	// 月線方向
variable: aveDir1200(0);aveDir1200 = Sign(ave1200 - ave1200[1]);// 季線方向

// 計算MACD
input: FastLength(15), SlowLength(19), MACDLength(10);
variable: difValue(0), macdValue(0), oscValue(0);
MACD(weightedclose(), FastLength, SlowLength, MACDLength, difValue, macdValue, oscValue);
variable: macdDir(0); macdDir = Sign(macdValue - macdValue[2]);		// MACD 方向
variable: difDir(0); difDir = Sign(difValue - difValue[2]);			// DIF 方向 (向下 => -1)
variable: macdUnder(0); macdUnder = Sign(difValue - macdValue);		// DIF 和 MACD 的上下位置

print("= 富-長多-出 ",DateToString(todayDate)," ",TimeToString(todayTime)," ",NumToStr(close, 0)," =");

// 富-長多-出 策略2: 月線空頭提前出場
// 1. 月線連續向下
// 2. DIF 3向下
// 3. 指數低於 18小時K
// 4. 指數低於月線
condition21 = aveDir400 = -1 and aveDir400[1] = -1 and aveDir400[2] = -1 and aveDir400[3] = -1 and aveDir400[4] = -1 and aveDir400[5] = -1;
condition22 = difDir = -1 and difDir[1] = -1 and difDir[2] = -1;
condition23 = close < ave72;
condition24 = close < ave400;

print("富-長多-出 策略2: ", condition21, condition22, condition23, condition24, "月線空頭提前出場");
if condition21 and condition22 and condition23 and condition24 then
begin
	ret = 1;
end;

// 富-長多-出 策略1: 72k線DIF向下
// 1. 72K線趨勢向下
// 2. DIF向下連續3k向下
// 3. 指數低於72k線K 3K
condition11 = aveDir72 = -1 and aveDir72[1] = -1 and aveDir72[2] = -1;
condition12 = difDir = -1 and difDir[1] = -1 and difDir[2] = -1;
condition13 = close < ave72 and close[1] < ave72[2] and close[3] < ave72[3];

print("富-長多-出 策略1: ", condition11, condition12, condition13, "72k線DIF向下");
if condition11 and condition12 and condition13 then
begin
	ret = 1;
end;




