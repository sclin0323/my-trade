if barinterval <> 15 then RaiseRunTimeError("請設定頻率為15分鐘");
variable: todayDate(0); todayDate = GetField("Date");
variable: todayTime(0); todayTime = GetField("Time"); 

input: costPrice(0);	// 我的成本
if costPrice = 0 then begin 
	print("請輸入目前成本!!");
	return;
end; 
  
// 計算均線  
variable: ave72(0); 	ave72 = Average(Close,72);			// 18小時K (3~4天)
variable: ave1200(0); 	ave1200 = Average(Close,1200);		// 季線
variable: aveDir72(0);	aveDir72 = Sign(ave72 - ave72[1]);	// 18小時均線方向
variable: aveDir1200(0);	aveDir1200 = Sign(ave1200 - ave1200[1]);	// 季線方向

// 計算MACD
input: FastLength(15), SlowLength(19), MACDLength(10);
variable: difValue(0), macdValue(0), oscValue(0);
MACD(weightedclose(), FastLength, SlowLength, MACDLength, difValue, macdValue, oscValue);
variable: macdDir(0); macdDir = Sign(macdValue - macdValue[2]);		// MACD 方向
variable: difDir(0); difDir = Sign(difValue - difValue[2]);			// DIF 方向 (向下 => -1)
variable: macdUnder(0); macdUnder = Sign(difValue - macdValue);		// DIF 和 MACD 的上下位置

print("= 富-長空-出 ",DateToString(todayDate)," ",TimeToString(todayTime)," ",NumToStr(close, 0)," =");

// 富-長多-出 策略3: 沒有獲利,長空提前出場
// 1. 沒有獲利
// 2. 72K線趨勢向上
// 3. DIF向上
// 4. 指數高於72k線K
condition31 = close - costPrice > 0;
condition32 = aveDir72 = 1;
condition33 = difDir = 1;
condition34 = close > ave72;

print("富-長多-出 策略3: ", condition31, condition32, condition33, condition34, "沒有獲利,長空提前出場");
if condition31 and condition32 and condition33 and condition34 then
begin
	ret = 1;
end;

// 富-長空-出 策略2: 遇上升季線, 提早出場
// 1. 5小時內最低點 - 季線 < 30點(指數來到上升季線)
// 2. 季線向上
// 3. DIF 8k有6k向上
// 4. 5小時內反彈上漲超過40點
condition21 = Lowest(low, 20) - ave1200 < 30;
condition22 = aveDir1200 = 1;
condition23 = Summation(difDir,8) > 4 or Summation(difDir,8) = 4;
condition24 = close - Lowest(low, 20) > 40;

print("富-長空-出 策略2: ", condition21, condition22, condition23, condition24,"遇上升季線, 提早出場");
if condition21 and condition22 and condition23 and condition24 then
begin
	ret = 1;
end;

// 富-長空-出 策略1: 72k線DIF向上
// 1. 72K線連續3k趨勢向上
// 2. DIF向上連續3k向上
// 3. 指數高於72k線K連續3k
condition11 = aveDir72 = 1 and aveDir72[1] = 1 and aveDir72[2] = 1;
condition12 = difDir = 1 and difDir[1] = 1 and difDir[2] = 1;
condition13 = close > ave72 and close[1] > ave72[1] and close[2] > ave72[2];

print("富-長空-出 策略1: ", condition11, condition12, condition13,"72k線DIF向上");
if condition11 and condition12 and condition13 then
begin
	ret = 1;
end;

