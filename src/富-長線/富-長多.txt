// 執行頻率需為 60 分鐘
if barinterval <> 60 then RaiseRunTimeError("請設定頻率為60分鐘");
variable: todayDate(0); todayDate = GetField("Date"); 
variable: todayTime(0); todayTime = GetField("Time");
     
// 計算均線
variable: ave2(0); 		ave2 = Average(Close,2);			// 2小時均線
variable: ave18(0); 	ave18 = Average(Close,18);			// 18小時均線
variable: ave90(0); 	ave90 = Average(Close,90);			// 90小時均線
variable: ave300(0); 	ave300 = Average(Close,300);		// 300小時均線(季線)
variable: aveYear(0); 	aveYear = Average(Close,1200);		// 年線
variable: aveDir2(0);	aveDir2 = Sign(ave2 - ave2[1]);		// 2小時均線方向	
variable: aveDir18(0);	aveDir18 = Sign(ave18 - ave18[1]);	// 18小時均線方向
variable: aveDir300(0);	aveDir300 = Sign(ave300 - ave300[1]);	// 300小時均線方向(季線)
variable: aveDirYear(0);aveDirYear = Sign(aveYear - aveYear[1]);	// 年線方向


// 計算MACD
input: FastLength(12), SlowLength(26), MACDLength(9);	// 依照XQ軟體預設值
variable: difValue(0), macdValue(0), oscValue(0);
MACD(weightedclose(), FastLength, SlowLength, MACDLength, difValue, macdValue, oscValue);
variable: macdDir(0); macdDir = Sign(macdValue - macdValue[2]);		// MACD 方向
variable: difDir(0); difDir = Sign(difValue - difValue[2]);			// DIF 方向 (向下 => -1)
variable: macdUnder(0); macdUnder = Sign(difValue - macdValue);		// DIF 和 MACD 的上下位置

// 盤中資訊
print("==============================================================");
print("aveDir18 分數(需大於7):",NumToStr(Summation(aveDir18,15),0));
print("DIF方向(需為正向):",difDir);
print("指數(大於ave18):",NumToStr(close, 0)," ave18:",NumToStr(ave18,0));
print("2小時均線方向(需為正向):",aveDir2);
print("指數與90小時線差(需小於150):",close - ave90);
print("季線(90小時)方向:",aveDir300);

// 富-長多 策略3 (2小時線穿頭18k做多)
// 1. 過去15K當中 aveDir18 出現5or6K 向上
// 2. DIF向上
// 3. 指數高於 ave18
// 4. 2小時均線方向 = 1
// 5. 90小時乖離要小於150
// 6. 2小時線均線穿過18小時線
condition31 = Summation(aveDir18,15) = -1 or Summation(aveDir18,15) = -3;
condition32 = difDir = 1;
condition33 = close > ave18;
condition34 = aveDir2 = 1;
condition35 = close - ave90 < 150;
condition36 = (ave2 > ave18) and (ave2[1] < ave18[1] or ave2[2] < ave18[2] or ave2[3] < ave18[3]);
	
print("富-長多 策略3: ",DateToString(todayDate),TimeToString(todayTime), condition31, condition32, condition33, condition34, condition35, condition36 );
if condition31 and condition32 and condition33 and condition34 and condition35 and condition36 then
begin
	ret = 1;
end;


// 富-長多 策略2 (拉回季線,季線向上 寬鬆買點)
// 1. 季線向上 and 指數過去3天有低於季線+35點
// 2. 過去15K當中 aveDir18 為 1 ~ 11(之間)
// 3. DIF向上
// 4. 指數高於 ave18
// 5. 2小時均線方向 = 1
// 6. 90小時乖離要小於150
// 7. 年線向上
condition21 = aveDir300 = 1 and aveDir300[1] = 1 and aveDir300[2] = 1 and aveDir300[3] = 1 and aveDir300[4] = 1 and aveDir300[5] = 1 and aveDir300[6] = 1 and (ave300 + 35 > Lowest(low, 15));
condition22 = Summation(aveDir18,15) > 0 and Summation(aveDir18,15) < 12;//(Summation(aveDir18,15) > 3 or Summation(aveDir18,15) = 3);
condition23 = difDir = 1;
condition24 = close > ave18;
condition25 = aveDir2 = 1;
condition26 = close - ave90 < 150;
condition27 = aveDirYear = 1;

print("富-長多 策略2: ",DateToString(todayDate),TimeToString(todayTime), condition21, condition22, condition23, condition24, condition25, condition26, condition27 );
if condition21 and condition22 and condition23 and condition24 and condition25 and condition26 and condition27 then
begin
	ret = 1;
end;


// 富-長多 策略1
// 1. 過去15K當中 aveDir18 有超過11K向上
// 2. DIF向上
// 3. 指數高於 ave18
// 4. 2小時均線方向 = 1
// 5. 90小時乖離要小於150
condition11 = (Summation(aveDir18,15) > 7 or Summation(aveDir18,15) = 7);
condition12 = difDir = 1;
condition13 = close > ave18;
condition14 = aveDir2 = 1;
condition15 = close - ave90 < 150;
	
print("富-長多 策略1: ",DateToString(todayDate),TimeToString(todayTime), condition11, condition12, condition13, condition14, condition15 );
if condition11 and condition12 and condition13 and condition14 and condition15 then
begin
	ret = 1;
end;




