if barinterval <> 15 then RaiseRunTimeError("請設定頻率為15分鐘");
variable: todayDate(0); todayDate = GetField("Date");
variable: todayTime(0); todayTime = GetField("Time");  
  
input: costPrice(0);	// 我的成本
if costPrice = 0 then begin 
	print("請輸入目前成本!!");
	return;
end;

// 計算均線  
variable: ave135(0); 	ave135 = Average(Close,9);				// 135分均線
variable: aveDir135(0);	aveDir135 = Sign(ave135 - ave135[1]);	// 135分均線方向


// 計算MACD
input: FastLength(15), SlowLength(19), MACDLength(10);	// 依照XQ軟體預設值
variable: difValue(0), macdValue(0), oscValue(0);
MACD(weightedclose(), FastLength, SlowLength, MACDLength, difValue, macdValue, oscValue);
variable: macdDir(0); macdDir = Sign(macdValue - macdValue[2]);		// MACD 方向
variable: difDir(0); difDir = Sign(difValue - difValue[2]);			// DIF 方向 (向下 => -1)
variable: macdUnder(0); macdUnder = Sign(difValue - macdValue);		// DIF 和 MACD 的上下位置

// 大盤多空判斷
value1 = GetQuote("委買均") - GetQuote("委賣均");		// 均口差(成交)     作多: 數值越大越好
value2 = GetQuote("累計委買") - GetQuote("累計委賣");	// 委買口數差(成交) 作多: 數值越大越好
value3 = GetQuote("累買成筆") - GetQuote("累賣成筆");	// 成交買賣差(成交) 作多: 數值越小越好
Value4 = GetQuote("標的漲跌幅");

print("= 多-平倉 ",DateToString(todayDate)," ",TimeToString(todayTime)," ",NumToStr(close, 0), GetQuote("當日漲幅%")," | DIF值:",NumToStr(difValue,1),"DIF方向:",NumToStr(difDir,0),"DIF21k:",NumToStr(Summation(difDir,21),0)," | ",NumToStr(value1, 2)," ",NumToStr(value2, 0)," ",NumToStr(value3, 0)," ", NumToStr(value4, 2)," =");

// 時間: 08:40 to 13:45
// 小獲利漲勢不足出場, 預防一天漲一天跌 (出場後立即啟動短線策略)
// 1. 獲利25點之內
// 2. 135分均線向下
if todayTime >= 084000 and todayTime < 134500 then
begin
	condition21 = close - costPrice < 25;
	condition22 = aveDir135 = -1;
	
	print("多-平倉-策略2: ", condition21, condition22);
	if condition21 and condition22 then
	begin
		ret = 1;
	end;
end;



// 時間: 08:55 to 13:45
// 大盤由多逐漸走空出場 (適合停利使用)
// 1. 如果紅盤 => 過去21k線內 DIF值有超過15k(包含)向下(Summation 小於-11) Or 如果黑盤 => 過去21k線內 DIF值有超過15k(包含)向下(Summation 小於-9)
// 2. DIF向下
// 3. 指數低於135分均線
// 4. 指數比過去21K線最高 跌點超過25點
// 5. 135分均線方向向下
if todayTime >= 085500 and todayTime < 134500 then
begin
	condition11 = (GetQuote("當日漲幅%") > 0 and ((Summation(difDir,21) < -11 or Summation(difDir,21) = -11))) or (GetQuote("當日漲幅%") < 0 and ((Summation(difDir,21) < -9 or Summation(difDir,21) = -9)));
	condition12 = difDir = -1;
	condition13 = close < ave135;
	condition14 = highest(high, 21) - close > 25;
	condition15 = aveDir135 = -1;
	
	
	print("多-平倉-策略1: ", condition11, condition12, condition13, condition14, condition15);
	if condition11 and condition12 and condition13 and condition14 and condition15 then
	begin
		ret = 1;
	end;
end;
