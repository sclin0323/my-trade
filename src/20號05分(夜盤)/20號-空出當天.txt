if barinterval <> 1 then RaiseRunTimeError("請設定頻率為1分鐘");
variable: todayDate(0); todayDate = GetField("Date");
variable: todayTime(0); todayTime = GetField("Time"); 
 
// 我的成本    
input: cost(0);	  
if cost = 0 then begin 
	print("請輸入目前成本!!");
	return;
end;

// 計算均線  
variable: ave8(0); 		ave8 = Average(Close,8);	// 8分鐘均線
variable: ave17(0); 	ave17 = Average(Close,17);	// 17分鐘均線
variable: ave34(0); 	ave34 = Average(Close,34);	// 34分鐘均線
variable: ave68(0); 	ave68 = Average(Close,68);	// 68分鐘均線
variable: ave130(0); 	ave130 = Average(Close,130);	// 130分鐘均線
variable: aveDir8(0);	aveDir8 = Sign(ave8 - ave8[1]);		// 8分鐘均線方向
variable: aveDir17(0);	aveDir17 = Sign(ave17 - ave17[1]);	// 17分鐘均線方向
variable: aveDir34(0);	aveDir34 = Sign(ave34 - ave34[1]);	// 34分鐘均線方向
variable: aveDir68(0);	aveDir68 = Sign(ave68 - ave68[1]);	// 68分鐘均線方向
variable: aveDir130(0);	aveDir130 = Sign(ave130 - ave130[1]);	// 130分鐘均線方向

// 計算MACD
input: FastLength(18), SlowLength(32), MACDLength(12);	// 依照XQ軟體預設值
variable: difValue(0), macdValue(0), oscValue(0);
MACD(weightedclose(), FastLength, SlowLength, MACDLength, difValue, macdValue, oscValue);
variable: macdDir(0); macdDir = Sign(macdValue - macdValue[2]);		// MACD 方向
variable: difDir(0); difDir = Sign(difValue - difValue[2]);			// DIF 方向 (向下 => -1)
variable: macdUnder(0); macdUnder = Sign(difValue - macdValue);		// DIF 和 MACD 的上下位置

// 大盤多空判斷
value1 = GetQuote("累計委買") - GetQuote("累計委賣");   // 委買口數差(成交) 作多: 數值越大越好
value2 = GetQuote("累買成筆") - GetQuote("累賣成筆");   // 成交買賣差(成交) 作多: 數值越小越好
value3 = GetQuote("當日漲幅%");
print("= 夜短空-出 ",DateToString(todayDate)," ",TimeToString(todayTime)," ",NumToStr(close, 0),NumToStr(value2, 0),Summation(aveDir68,15)," =");
 

// 1. 跌幅沒超過-0.3 and 收盤前獲利小於20點出場
// 2. 跌幅超過0.3放寬5點
if (todayTime >= 043500 and todayTime < 045900) then
begin
	condition41 = GetQuote("當日漲幅%") > -0.3 and cost - close < 20;
	condition42 = GetQuote("當日漲幅%") < -0.3 and cost - close < 15;
	print("策略4: ", condition41, condition42);
	if condition41 or condition42 then
	begin
		ret = 1; return;
	end;
end;



// 策略2: 收盤前中線趨勢向上=>不留倉
// 1. 130分鐘均線趨勢向上
// 2. 過去25分鐘內有8分鐘 
// 3. 指數高於130分均線
// 4. 130分鐘均線趨勢向上 and DIF向上
// 5. 獲利小於25點
// 6. 過去130分鐘內上漲超過15點 or 獲利4-10點
condition21 = aveDir130 = 1;
condition22 = Summation(aveDir130,25) > 9;
condition23 = close < ave130;
condition24 = aveDir130 = -1 and difDir = -1 and difDir[1] = -1;
condition25 = close - cost < 25;
condition26 = (close - lowest(low, 130) > 15) or (cost - close < 10 and cost - close > 4);
print("策略2: ", condition21, condition22, condition23, condition24, condition25, condition26);
if condition21 and condition22 and condition23 and condition24 and condition25 and condition26 then
begin
	ret = 1; return;
end;

// 策略1: 小點停損停利出場
// 1. 68分線過去15分鐘有超過11分鐘向上
// 2. 指數高於68分線
// 3. 獲利連續2分鐘低於15點
// 4. DIF向上
// 5. 過去130分鐘內上漲超過15點 or 獲利4-10點
condition11 = (Summation(aveDir68,15) > 7 or Summation(aveDir68,15) = 7);
condition12 = close > ave68;
condition13 = (cost - close < 15) and (cost - close[1] < 15);
condition14 = difDir = 1 and difDir[1] = 1;
condition15 = close - lowest(low, 130) > 15 or (cost - close < 10 and cost - close > 4);
print("策略1: ", condition11, condition12, condition13, condition14, condition15);
if condition11 and condition12 and condition13 and condition14 and condition15 then
begin
	ret = 1; return;
end;




