if barinterval <> 1 then RaiseRunTimeError("請設定頻率為1分鐘");
variable: todayDate(0); todayDate = GetField("Date");
variable: todayTime(0); todayTime = GetField("Time"); 
      
// 我的成本     
input: cost(0);	
if cost = 0 then begin 
	print("請輸入目前成本!!");
	return;
end;  

// 計算均線  
variable: ave8(0); 		ave8 = Average(Close,8);		// 8分鐘均線
variable: ave17(0); 	ave17 = Average(Close,17);		// 17分鐘均線
variable: ave34(0); 	ave34 = Average(Close,34);		// 34分鐘均線
variable: ave68(0); 	ave68 = Average(Close,68);		// 68分鐘均線
variable: ave130(0); 	ave130 = Average(Close,130);	// 130分鐘均線
variable: aveDir8(0);	aveDir8 = Sign(ave8 - ave8[1]);		// 8分鐘均線方向
variable: aveDir17(0);	aveDir17 = Sign(ave17 - ave17[1]);	// 17分鐘均線方向
variable: aveDir34(0);	aveDir34 = Sign(ave34 - ave34[1]);	// 34分鐘均線方向
variable: aveDir68(0);	aveDir68 = Sign(ave68 - ave68[1]);	// 68分鐘均線方向
variable: aveDir130(0);	aveDir130 = Sign(ave130 - ave130[1]);	// 130分鐘均線方向

// 計算MACD
input: FastLength(18), SlowLength(32), MACDLength(12);	// 依照XQ軟體預設值
variable: difValue(0), macdValue(0), oscValue(0);
MACD(weightedclose(), FastLength, SlowLength, MACDLength, difValue, macdValue, oscValue);
variable: macdDir(0); macdDir = Sign(macdValue - macdValue[2]);		// MACD 方向
variable: difDir(0); difDir = Sign(difValue - difValue[2]);			// DIF 方向 (向下 => -1)
variable: macdUnder(0); macdUnder = Sign(difValue - macdValue);		// DIF 和 MACD 的上下位置

print("= 多-平倉當天 ",DateToString(todayDate)," ",TimeToString(todayTime)," ",NumToStr(close, 0)," =");

// 1. 漲幅沒超過0.3 and 收盤前獲利小於15點出場
// 2. 漲幅超過0.3 and 收盤前獲利小於10點出場
if (todayTime >= 043500 and todayTime < 045900) then
begin
	condition41 = GetQuote("當日漲幅%") < 0.3 and close - cost < 15;
	condition42 = GetQuote("當日漲幅%") > 0.3 and close - cost < 10;
	print("策略4: ", condition41, condition42);
	if condition41 or condition42 then
	begin
		ret = 1; return;
	end;
end;


// 策略3: 小點停損停利出場 (寬鬆是因為進場時有參照, 已經避開修正風險)
// 1. 68分線連續3分鐘向下
// 2. 指數低於68分線
// 3. 獲利連續2分鐘低於15點
// 4. DIF向下 
// 5. 130線向下 or 平盤之下
// 6. 過去130分鐘內下跌超過15點 or 獲利4-10點
condition31 = aveDir68 = -1 and aveDir68[1] = -1 and aveDir68[2] = -1;
condition32 = close < ave68;
condition33 = (close - cost < 15) and (close[1] - cost < 15);
condition34 = difDir = -1;
condition35 = aveDir130 = -1 or GetQuote("當日漲幅%") < 0;
condition36 = (highest(high, 130) - close > 15) or (close - cost < 10 and close - cost > 4);
print("多-平倉當天-策略7: ", condition31, condition32, condition33, condition34, condition35, condition36);
if condition31 and condition32 and condition33 and condition34 and condition35 and condition36 then
begin
	ret = 1; return;
end;

// 策略3: 收盤前中線趨勢逆勢出場
// 1. 過去25分鐘內有8分鐘 130分鐘均線趨勢向下
// 2. 指數低於130分均線
// 3. 130分鐘均線趨勢向下 and DIF向下
// 4. 獲利小於25點
// 5. 過去130分鐘內下跌超過15點 or 獲利4-10點
condition21 = Summation(aveDir130,25) < 9;
condition22 = close < ave130;
condition23 = aveDir130 = -1 and difDir = -1;
condition24 = close - cost < 25;
condition25 = (highest(high, 130) - close > 15) or (close - cost < 10 and close - cost > 4);
print("多-平倉當天-策略3: ", condition21, condition22, condition23, condition24, condition25);
if condition21 and condition22 and condition23 and condition24 and condition25 then
begin
	ret = 1; return;
end;


// 策略1: 小點停損停利出場
// 1. 68分線連續3分鐘向下
// 2. 指數低於68分線
// 3. 獲利連續2分鐘低於15點
// 4. DIF向下 
// 5. 過去130分鐘內下跌超過15點 or 獲利4-10點
condition11 = aveDir68 = -1 and aveDir68[1] = -1 and aveDir68[2] = -1;
condition12 = close < ave68 and close[1] < ave68;
condition13 = (close - cost < 15) and (close[1] - cost < 15);
condition14 = difDir = -1 and difDir[1] = -1;
condition15 = (highest(high, 130) - close > 15) or (close - cost < 10 and close - cost > 4);
print("多-平倉當天-策略1: ", condition11, condition12, condition13, condition14, condition15);
if condition11 and condition12 and condition13 and condition14 and condition15 then
begin
	ret = 1; return;
end;