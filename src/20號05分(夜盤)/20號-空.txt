if barinterval <> 5 then RaiseRunTimeError("請設定頻率為5分鐘");
variable: todayDate(0); todayDate = GetField("Date");
variable: todayTime(0); todayTime = GetField("Time"); 
 
// 計算均線  
variable: ave27(0);      ave27 = Average(Close,27);    			// 27K均線
variable: ave81(0);      ave81 = Average(Close,81);    			// 81K均線
variable: aveDir27(0);   aveDir27 = Sign(ave27 - ave27[1]);     // 81K均線方向
variable: aveDir81(0);   aveDir81 = Sign(ave81 - ave81[1]);     // 81K均線方向

// 計算MACD
input: FastLength(12), SlowLength(26), MACDLength(9);	// 依照XQ軟體預設值
variable: difValue(0), macdValue(0), oscValue(0);
MACD(weightedclose(), FastLength, SlowLength, MACDLength, difValue, macdValue, oscValue);
variable: macdDir(0); macdDir = Sign(macdValue - macdValue[2]);     // MACD 方向
variable: difDir(0); difDir = Sign(difValue - difValue[2]);         // DIF 方向 (向下 => -1)
variable: macdUnder(0); macdUnder = Sign(difValue - macdValue);     // DIF 和 MACD 的上下位置

print("3號-空",DateToString(todayDate),TimeToString(todayTime));


// 夜盤操作時間: 09:40 to 02:30
if (not ((todayTime >= 214000 and todayTime <= 235900) or (todayTime >= 000000 and todayTime < 023000))) then
begin
	print("夜盤操作時間: 09:40 to 02:30!!");
	return;
end;

////////////////////////////////////////////////////////////////////////////////////////////////

// 策略5: 蓋頭逐漸走空
// 1. 27 81 連續9K向下
// 2. 指數跌破81 18K
// 3. 指數跌破最近6K
// 4. 27 < 81 18k
// 5. 指數離81 15點之內
// 6. DIF向下 and DIF介於-0.5to-3.5
// 7. 過去54K下跌超過15點
condition51 = aveDir27 = -1 and aveDir27[3] = -1 and aveDir27[6] = -1 and aveDir27[9] = -1 and aveDir81 = -1 and aveDir81[3] = -1 and aveDir81[6] = -1 and aveDir81[9] = -1;
condition52 = close < ave81 and close[3] < ave81[3] and close[6] < ave81[6] and close[9] < ave81[9] and close[12] < ave81[12] and close[15] < ave81[15] and close[18] < ave81[18];
condition53 = close < lowest(close[1],6);
condition54 = ave27 < ave81 and ave27[6] < ave81[6] and ave27[12] < ave81[12] and ave27[18] < ave81[18];
condition55 = ave81 - close < 15;
condition56 = difDir = -1 and (difValue < -1 and difValue > -4);
condition57 = highest(high, 54) - close > 15;
print("策略5:", condition51, condition52, condition53, condition54, condition55, condition56, condition57);
if condition51 and condition52 and condition53 and condition54 and condition55 and condition56 and condition57 then
begin
    ret = 1; return;
end;

// 策略4: 震盪走弱 突然快速加空
// 1. 27 81向下(6K)
// 2. 過去27K 指數都低於81
// 3. 指數跌破過去27K
// 4. 指數離81 20點內
// 5. 指數 27 81 空頭排列
// 6. DIF向下 and DIF介於-1to-4
// 7. 過去54K下跌超過20點
condition41 = aveDir27 = -1 and aveDir27[3] = -1 and aveDir27[6] = -1 and aveDir81 = -1 and aveDir81[3] = -1 and aveDir81[6] = -1;
condition42 = close < ave81 and close[5] < ave81[5] and close[10] < ave81[10] and close[15] < ave81[15] and close[20] < ave81[20] and close[27] < ave81[27];
condition43 = close < lowest(close[1],27);
condition44 = ave81 - close < 20;
condition45 = close < ave27 and ave27 < ave81;
condition46 = difDir = -1 and (difValue < -1 and difValue > -4);
condition47 = highest(high, 54) - close > 20;
print("策略4:", condition41, condition42, condition43, condition44, condition45, condition46, condition47);
if condition41 and condition42 and condition43 and condition44 and condition45 and condition46 and condition47 then
begin
    ret = 1; return;
end;

// 策略3: 貫穿81均線全面走空
// 1. 27 81向下(2K)
// 2. 指數 低於27 81
// 3. 指數 27 81 15點之內
// 4. DIF向下 and DIF介於0.2to-2.8
// 5. 過去27K下跌超過25點
condition31 = (aveDir27 = -1 and aveDir81 = -1) or (aveDir27[1] = -1 and aveDir81[1] = -1);
condition32 = close < ave27 and close < ave81;
condition33 = MaxList(close, ave27, ave81) - MinList(close, ave27, ave81) < 15;
condition34 = difDir = -1 and (difValue < 0.2 and difValue > -2.8);
condition35 = highest(high, 27) - close > 25;
print("策略3:", condition31, condition32, condition33, condition34, condition35);
if condition31 and condition32 and condition33 and condition34 and condition35 then
begin
    ret = 1; return;
end;

// 策略2: 指數逐步走空
// 1. 27均線連續5K向下
// 2. 81均線連續5K向下
// 3. 指數 27 81 空頭排列
// 4. 指數 81 40點之內
// 5. DIF向下 and DIF介於-7to-10
// 6. 過去27K下跌超過30點
condition21 = aveDir27 = -1 and aveDir27[1] = -1 and aveDir27[2] = -1 and aveDir27[3] = -1 and aveDir27[4] = -1;
condition22 = aveDir81 = -1 and aveDir81[1] = -1 and aveDir81[2] = -1 and aveDir81[3] = -1 and aveDir81[4] = -1;
condition23 = close < ave27 and ave27 < ave81;
condition24 = ave81 - close < 40;
condition25 = difDir = -1 and (difValue < -7 and difValue > -10);
condition26 = highest(high, 27) - close > 30;
print("策略2:", condition21, condition22, condition23, condition24, condition25, condition26);
if condition21 and condition22 and condition23 and condition24 and condition25 and condition26 then
begin
    ret = 1; return;
end;

// 策略1: 指數跌破81均線走空
// 1. 81均線連續3K向下
// 2. 指數連3K低於27
// 3. 指數6K中跌破82
// 4. 指數 27 82三點差距35內
// 5. DIF向下 and DIF介於-0.2to-3.2
// 6. 過去27K下跌超過30點
condition11 = aveDir81 = -1 and aveDir81[1] = -1 and aveDir81[2] = -1;
condition12 = close < ave27 and close[1] < ave27[1] and close[2] < ave27[2];
condition13 = close < ave81 and close[6] > ave81[6];
condition14 = MaxList(close, ave27, ave81) - MinList(close, ave27, ave81) < 35;
condition15 = difDir = -1 and (difValue < -0.2 and difValue > -3.2);
condition16 = highest(high, 27) - close > 30;
print("策略1:", condition11, condition12, condition13, condition14, condition15, condition16);
if condition11 and condition12 and condition13 and condition14 and condition15 and condition16 then
begin
    ret = 1; return;
end;