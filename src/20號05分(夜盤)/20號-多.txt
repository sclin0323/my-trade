if barinterval <> 5 then RaiseRunTimeError("請設定頻率為5分鐘");
variable: todayDate(0); todayDate = GetField("Date");
variable: todayTime(0); todayTime = GetField("Time");  
 
// 計算均線  
variable: ave27(0);      ave27 = Average(Close,27);    			// 27K均線
variable: ave81(0);      ave81 = Average(Close,81);    			// 81K均線
variable: aveDir27(0);   aveDir27 = Sign(ave27 - ave27[1]);     // 81K均線方向
variable: aveDir81(0);   aveDir81 = Sign(ave81 - ave81[1]);     // 81K均線方向

// 計算MACD
input: FastLength(12), SlowLength(26), MACDLength(9);	// 依照XQ軟體預設值
variable: difValue(0), macdValue(0), oscValue(0);
MACD(weightedclose(), FastLength, SlowLength, MACDLength, difValue, macdValue, oscValue);
variable: macdDir(0); macdDir = Sign(macdValue - macdValue[2]);     // MACD 方向
variable: difDir(0); difDir = Sign(difValue - difValue[2]);         // DIF 方向 (向下 => -1)
variable: macdUnder(0); macdUnder = Sign(difValue - macdValue);     // DIF 和 MACD 的上下位置

print("3號-多",DateToString(todayDate),TimeToString(todayTime));


// 夜盤操作時間: 09:40 to 02:30
if (not ((todayTime >= 214000 and todayTime <= 235900) or (todayTime >= 000000 and todayTime < 023000))) then
begin
	print("夜盤操作時間: 09:40 to 02:30!!");
	return;
end;

////////////////////////////////////////////////////////////////////////////////////////////////

// 策略4: 創新高 and 緩緩逐步上漲
// 1. 27 連續9K向上
// 2. 81 連續2K向上
// 3. 指數站上 36k 最高
// 4. 27 > 81 6k
// 5. 指數離81 15點之內
// 6. DIF向上 and DIF介於0.5to3.5
condition41 = aveDir27 = 1 and aveDir27[3] = 1 and aveDir27[5] = 1 and aveDir27[7] = 1 and aveDir27[9] = 1;
condition42 = aveDir81 = 1 and aveDir81[2] = 1;
condition43 = close > highest(close[3], 36);
condition44 = ave27 > ave81 and ave27[2] > ave81[2] and ave27[4] > ave81[4] and ave27[6] > ave81[6];
condition45 = close - ave81 < 15;
condition46 = difDir = 1 and (difValue > 0.5 and difValue < 3.5);
print("策略4:", condition41, condition42, condition43, condition44, condition45, condition46);
if condition41 and condition42 and condition43 and condition44 and condition45 and condition46 then
begin
    ret = 1; return;
end;

// 策略3: 緩緩逐步上漲
// 1. 27 81 連續5K向上
// 2. 指數站上27 9K
// 3. 指數站上81 12K
// 4. 27 > 81 12k
// 5. 指數離81 15點之內
// 6. DIF向上 and DIF介於0to3
condition31 = aveDir27 = 1 and aveDir27[2] = 1 and aveDir27[3] = 1 and aveDir27[4] = 1 and aveDir27[5] = 1 and aveDir81 = 1 and aveDir81[2] = 1 and aveDir81[3] = 1 and aveDir81[4] = 1 and aveDir81[5] = 1;
condition32 = close > ave27 and close[3] > ave27[3] and close[6] > ave27[6] and close[9] > ave27[9];
condition33 = close > ave81 and close[3] > ave81[3] and close[6] > ave81[6] and close[9] > ave81[9] and close[12] > ave81[12];
condition34 = ave27 > ave81 and ave27[3] > ave81[3] and ave27[6] > ave81[6] and ave27[9] > ave81[9] and ave27[12] > ave81[15];
condition35 = close - ave81 < 15;
condition36 = difDir = 1 and (difValue > 0 and difValue < 3);
print("策略3:", condition31, condition32, condition33, condition34, condition35, condition36);
if condition31 and condition32 and condition33 and condition34 and condition35 and condition36 then
begin
    ret = 1; return;
end;

// 策略2: 突破81線後高檔震盪整理再上
// 1. 27 81 連續12K向上
// 2. 指數站上81 連續12K
// 3. ave27 > ave81 8k
// 4. 指數 > ave27 6K
// 5. 指數離81 30點內
// 6. DIF向上 and DIF介於4.5to7.5
condition21 = aveDir27 = 1 and aveDir27[4] = 1 and aveDir27[8] = 1 and aveDir27[12] = 1 and aveDir81 = 1 and aveDir81[4] = 1 and aveDir81[8] = 1 and aveDir81[12] = 1;
condition22 = close > ave81 and close[3] > ave81[3] and close[6] > ave81[6] and close[9] > ave81[9] and close[12] > ave81[12];
condition23 = ave27 > ave81 and ave27[2] > ave81[2] and ave27[4] > ave81[4] and ave27[6] > ave81[6] and ave27[8] > ave81[8];
condition24 = close > ave27 and close[2] > ave27[2] and close[4] > ave27[4] and close[6] > ave27[6];
condition25 = close - ave81 < 30;
condition26 = difDir = 1 and (difValue > 4 and difValue < 7);
print("策略2:", condition21, condition22, condition23, condition24, condition25, condition26);
if condition21 and condition22 and condition23 and condition24 and condition25 and condition26 then
begin
    ret = 1; return;
end;

// 策略1: 27K線穿過81K and 81K轉上
// 1. 過去3K中81均線有空轉上
// 2. 過去27K中27均線有穿過81均線
// 3. 指數要站上27 and 27 > 81
// 4. 27和81差距10點 and 指數離81 15點
// 5. DIF向上 and DIF介於-1to2
condition11 = aveDir81 = 1 and (aveDir81[1] = -1 or aveDir81[2] = -1 or aveDir81[3] = -1);
condition12 = ave27 > ave81 and ave27[1] > ave81[1] and ave27[3] > ave81[3] and ave27[27] < ave81[27];
condition13 = close > ave27 and ave27 > ave81;
condition14 = ave27 - ave81 < 10 and close - ave81 < 15;
condition15 = difDir = 1 and (difValue > -1 and difValue < 2);
print("策略1:", condition11, condition12, condition13, condition14, condition15);
if condition11 and condition12 and condition13 and condition14 and condition15 then
begin
    ret = 1; return;
end;


