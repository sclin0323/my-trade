if barinterval <> 15 then RaiseRunTimeError("請設定頻率為15分鐘");
variable: todayDate(0); todayDate = GetField("Date");
variable: todayTime(0); todayTime = GetField("Time");  
      
input: costPrice(0);	// 我的成本
if costPrice = 0 then begin 
	print("請輸入目前成本!!");
	return;
end; 
   
// 計算均線  
variable: ave135(0); 	ave135 = Average(Close,9);				// 135分均線
variable: aveDir135(0);	aveDir135 = Sign(ave135 - ave135[1]);	// 135分均線方向


// 計算MACD
input: FastLength(15), SlowLength(19), MACDLength(10);	// 依照XQ軟體預設值
variable: difValue(0), macdValue(0), oscValue(0);
MACD(weightedclose(), FastLength, SlowLength, MACDLength, difValue, macdValue, oscValue);
variable: macdDir(0); macdDir = Sign(macdValue - macdValue[2]);		// MACD 方向
variable: difDir(0); difDir = Sign(difValue - difValue[2]);			// DIF 方向 (向下 => -1)
variable: macdUnder(0); macdUnder = Sign(difValue - macdValue);		// DIF 和 MACD 的上下位置

// 大盤多空判斷
//value2 = GetQuote("累計委買") - GetQuote("累計委賣");	// 委買口數差(成交) 作多: 數值越大越好
//value3 = GetQuote("累買成筆") - GetQuote("累賣成筆");	// 成交買賣差(成交) 作多: 數值越小越好
//Value4 = GetQuote("標的漲跌幅");

// 大盤多空判斷
value1 = GetQuote("累計委買") - GetQuote("累計委賣");   // 委買口數差(成交)
value2 = GetQuote("累買成筆") - GetQuote("累賣成筆");   // 成交買賣差(成交)
value3 = GetQuote("標的漲跌幅");
 
print("= 多-平倉 ",DateToString(todayDate)," ",TimeToString(todayTime)," ",NumToStr(close, 0), GetQuote("當日漲幅%")," | DIF值:",NumToStr(difValue,1),"DIF方向:",NumToStr(difDir,0),"DIF21k:",NumToStr(Summation(difDir,21),0)," | ",NumToStr(value1, 2)," ",NumToStr(value2, 0)," ",NumToStr(value3, 0)," =");

// 策略執行條件-1
// 1. 如果 value2 小於-2500 and 上漲0.3 表示多方力道很強 => 不要出 
// 2. 如果 大漲超過 0.9 表示多方力道很強 => 不要出 
condition1 = value2 < -2500 and value3 > 0.3;
condition2 = value3 > 0.9;
if condition1 or condition2 then
begin
    print("策略執行條件 不成立!!");
    return;
end;

////////////////////////////////////////////////////////////////////

// 時間: 08:55 to 13:45
// 獲利超過260點 and 215點停利出場
// 1. 過去3天內獲利超過260點 (近3天內獲利有超過200點)
// 2. 獲利小於215點(連續2k)
// 3. DIF向下
// 4. 135分均線方向向下
// 5. 漲幅要低於0.3以下
if todayTime >= 085500 and todayTime < 134500 then
begin
	condition61 = highest(high, 60) - costPrice > 260;
	condition62 = close - costPrice < 215 and (close[1] - costPrice < 215 or close[2] - costPrice < 215);
	condition63 = difDir = -1;
	condition64 = aveDir135 = -1;
	condition65 = GetQuote("當日漲幅%") < 0.3;
	
	print("多-平倉-策略6: ", condition61, condition62, condition63, condition64, condition65, "獲利超過260點 and 215點停利出場");
	if condition61 and condition62 and condition63 and condition64 and condition65 then
	begin
		ret = 1; return;
	end;
end;

// 時間: 08:55 to 13:45
// 獲利超過200點 and 155點停利出場
// 1. 過去3天內獲利超過200點 (近3天內獲利有超過200點)
// 2. 獲利小於155點(連續2k)
// 3. DIF向下
// 4. 135分均線方向向下
// 5. 漲幅要低於0.3以下
if todayTime >= 085500 and todayTime < 134500 then
begin
	condition51 = highest(high, 60) - costPrice > 200;
	condition52 = close - costPrice < 155 and (close[1] - costPrice < 155 or close[2] - costPrice < 155);
	condition53 = difDir = -1;
	condition54 = aveDir135 = -1;
	condition55 = GetQuote("當日漲幅%") < 0.3;
	
	print("多-平倉-策略5: ", condition51, condition52, condition53, condition54, condition55, "獲利超過200點 and 155點停利出場");
	if condition51 and condition52 and condition53 and condition54 and condition55 then
	begin
		ret = 1; return;
	end;
end;

// 時間: 08:55 to 13:45
// 獲利超過140點 and 100點停利出場
// 1. 過去3天內獲利超過140點 (近3天內獲利有超過140點)
// 2. 獲利小於100點
// 3. DIF向下
// 4. 135分均線方向向下
// 5. 漲幅要低於0.3以下
if todayTime >= 085500 and todayTime < 134500 then
begin
	condition41 = highest(high, 60) - costPrice > 140;
	condition42 = close - costPrice < 100 and (close[1] - costPrice < 100 or close[2] - costPrice < 100);
	condition43 = difDir = -1;
	condition44 = aveDir135 = -1;
	condition45 = GetQuote("當日漲幅%") < 0.3;
	
	print("多-平倉-策略4: ", condition41, condition42, condition43, condition44, condition45, "獲利超過140點 and 100點停利出場");
	if condition41 and condition42 and condition43 and condition44 and condition45 then
	begin
		ret = 1; return;
	end;
end;

// 時間: 08:55 to 13:45
// 獲利超過95點 and 65點停利出場
// 1. 過去3天內獲利超過95點 (近3天內獲利有超過95點)
// 2. 獲利小於65點
// 3. DIF向下
// 4. 135分均線方向向下 or 一小時大跌75點
if todayTime >= 085500 and todayTime < 134500 then
begin
	condition31 = highest(high, 60) - costPrice > 95;
	condition32 = close - costPrice < 65 and (close[1] - costPrice < 65 or close[2] - costPrice < 65);
	condition33 = difDir = -1;
	condition34 = aveDir135 = -1 or (highest(high,5) - close > 75);
	
	print("多-平倉-策略3: ", condition31, condition32, condition33, condition34, "獲利超過95點 and 65點停利出場");
	if condition31 and condition32 and condition33 and condition34 then
	begin
		ret = 1; return;
	end;
end;

// 時間: 08:40 to 13:45
// 小獲利漲勢不足出場, 預防一天漲一天跌 (出場後立即啟動短線策略)
// 1. 獲利25點之內
// 2. 135分均線向下 or 一小時大跌75點
if todayTime >= 084000 and todayTime < 134500 then
begin
	condition21 = close - costPrice < 25;
	condition22 = aveDir135 = -1 or (highest(high,5) - close > 75);
	
	print("多-平倉-策略2: ", condition21, condition22, "小獲利漲勢不足出場");
	if condition21 and condition22 then
	begin
		ret = 1; return;
	end;
end;

// 時間: 08:55 to 13:45
// 大盤由多逐漸走空出場 (適合停利使用)
// 1. 如果紅盤 => 過去21k線內 DIF值有超過15k(包含)向下(Summation 小於-11) Or 如果黑盤 => 過去21k線內 DIF值有超過15k(包含)向下(Summation 小於-9)
// 2. DIF向下
// 3. 指數低於135分均線
// 4. 指數比過去21K線最高 跌點超過40點
// 5. 135分均線方向向下
if todayTime >= 085500 and todayTime < 134500 then
begin
	condition11 = (GetQuote("當日漲幅%") > 0 and ((Summation(difDir,21) < -11 or Summation(difDir,21) = -11))) or (GetQuote("當日漲幅%") < 0 and ((Summation(difDir,21) < -9 or Summation(difDir,21) = -9)));
	condition12 = difDir = -1;
	condition13 = close < ave135;
	condition14 = highest(high, 21) - close > 40;
	condition15 = aveDir135 = -1;
	
	print("多-平倉-策略1: ", condition11, condition12, condition13, condition14, condition15,"大盤由多逐漸走空出場 (停利使用)");
	if condition11 and condition12 and condition13 and condition14 and condition15 then
	begin
		ret = 1; return;
	end;
end;
