// 執行頻率需為 1 分鐘
if barfreq <> "Min" or barinterval <> 1 then RaiseRunTimeError("請設定頻率為1分鐘");
variable: todayDate(0); todayDate = GetField("Date"); 
variable: todayTime(0); todayTime = GetField("Time");
               
// 計算均線     
variable: ave8(0);      ave8 = Average(Close,8);    // 8分鐘均線
variable: ave17(0);     ave17 = Average(Close,17);  // 17分鐘均線
variable: ave34(0);     ave34 = Average(Close,34);  // 34分鐘均線
variable: ave68(0);     ave68 = Average(Close,68);  // 68分鐘均線
variable: ave130(0);    ave130 = Average(Close,130);    		// 130分鐘均線
variable: aveDir8(0);   aveDir8 = Sign(ave8 - ave8[1]);     	// 8分鐘均線方向
variable: aveDir17(0);  aveDir17 = Sign(ave17 - ave17[1]);  	// 17分鐘均線方向
variable: aveDir34(0);  aveDir34 = Sign(ave34 - ave34[1]);  	// 34分鐘均線方向
variable: aveDir68(0);  aveDir68 = Sign(ave68 - ave68[1]);  	// 68分鐘均線方向
variable: aveDir130(0); aveDir130 = Sign(ave130 - ave130[1]);   // 130分鐘均線方向
  
// 計算MACD
input: FastLength(18), SlowLength(32), MACDLength(12);	// 依照XQ軟體預設值
variable: difValue(0), macdValue(0), oscValue(0);
MACD(weightedclose(), FastLength, SlowLength, MACDLength, difValue, macdValue, oscValue);
variable: macdDir(0); macdDir = Sign(macdValue - macdValue[2]);     // MACD 方向
variable: difDir(0); difDir = Sign(difValue - difValue[2]);         // DIF 方向 (向下 => -1)
variable: macdUnder(0); macdUnder = Sign(difValue - macdValue);     // DIF 和 MACD 的上下位置
 
// 大盤多空判斷
value1 = GetQuote("累計委買") - GetQuote("累計委賣");   // 委買口數差(成交) 作多: 數值越大越好
value2 = GetQuote("累買成筆") - GetQuote("累賣成筆");   // 成交買賣差(成交) 作多: 數值越小越好
value3 = GetQuote("當日漲幅%");

print("= 多 ",DateToString(todayDate),TimeToString(todayTime),"|","指數:",NumToStr(close, 0),"DIF值:",NumToStr(difValue,1),"DIF方向:",NumToStr(difDir,0),"|",NumToStr(value1, 2),NumToStr(value2, 0),value3," =");

// 策略執行條件-1
// 1. 130分鐘線向上
// 2. 指數高於130分鐘線5分鐘
// 3. value2 連續3分鐘 < -1000
condition1 = aveDir130 = 1;
condition2 = close > ave130 and close[1] > ave130[1] and close[2] > ave130[2] and close[3] > ave130[3] and close[4] > ave130[4];
condition3 = value2 < -1000 and value2[1] < -1000 and value2[2] < -1000;
if not(condition1 or condition2 or condition3) then
begin
    print("策略執行條件 不成立!!");
    return;
end;

// 策略執行條件-2: 避免漲幅過大追高
// 4. 漲幅超過0.8%
condition4 = GetQuote("當日漲幅%") > 0.8;
if condition4 then
begin
    print("漲幅已過大 不追高!!");
    return;
end;

///////////////////////////////////////////////////////////////////////////////////////////////////////

// 時間: 09:05 to 10:20
// 出手時機: 多方趨勢回測130線作多
// 1. value2 < -1100 and value3 > 0.25% (多方趨勢)
// 2. difDir連續2k向上 and difValue < 0.5 and 30分鐘內曾飆高超過2(回測)
// 3. 130分鐘均線方向已經連續30分鐘向上
// 4. 130分鐘均線 30分鐘內上升超過1.8 (陡升)
if todayTime >= 090500 and todayTime < 102000 then
begin
	condition131 = value2 < -1100 and GetQuote("當日漲幅%") > 0.25;
	condition132 = (difDir = 1 and difDir[1] = 1) and (difValue < 0.5) and (highest(difValue, 30) > 2);
	condition133 = aveDir130 = 1 and aveDir130[5] = 1 and aveDir130[10] = 1 and aveDir130[15] = 1 and aveDir130[20] = 1 and aveDir130[25] = 1 and aveDir130[30] = 1;
	condition134 = ave130 - ave130[30] > 1.8;

	print("策略13: ", condition131, condition132, condition133, condition134);
	if condition131 and condition132 and condition133 and condition134 then
	begin
		ret = 1; return;
	end;
end;


// 時間: 09:03 to 09:13
// 出手時機: 開低走高盤
// 1. 成交買賣差 < 10
// 2. DIF向上 and 黃金交叉
// 3. 指數高於17分線上 and 8 17分線都向上
// 4. 8分線低於68分線30點以上(表示開盤大跌)
// 5. 成交買賣差5分鐘前要大於10
if todayTime >= 090300 and todayTime < 091300 then
begin
    condition121 = value2 < 10;
    condition122 = difDir = 1 and macdUnder = 1;
    condition123 = close > ave17 and aveDir8 = 1 and aveDir17 = 1;
    condition124 = ave8 + 30 < ave68;
    condition125 = value2[5] > 10;
     
    print("策略12: ", condition121, condition122, condition123, condition124, condition125 );
    if condition121 and condition122 and condition123 and condition124 and condition125 then
    begin
        ret = 1; return;
    end;
end;
 
// 時間: 09:05 to 10:20
// 出手時機: 開小高突破創高
// 1. value2 < -1800
// 2. 委買口數差瞬間(3m)增加1500
// 3. 成交買賣差瞬間(3m)減少600
// 4. 四線向上
// 5. 四線多頭 and 指數在均線8之上
// 6. DIF 趨勢向上 and DIF 小於3.5
if todayTime >= 090500 and todayTime < 102000 then
begin
    condition111 = value2 < -1800;
    condition112 = (value1 - minlist(value1[1], value1[2], value1[3]) > 1500);
    condition113 = (maxlist(value2[1], value2[2], value2[3]) - value2 > 600);
    condition114 = (aveDir8 = 1) and (aveDir17 = 1) and (aveDir34 = 1) and (aveDir68 = 1);
    condition115 = (ave17 > ave34) and (ave34 > ave68) and close > ave8;
    condition116 = (difValue < 3.5) and (difDir = 1);
     
    print("策略11: ", condition111, condition112, condition113, condition114, condition115, condition116 );
    if condition111 and condition112 and condition113 and condition114 and condition115 and condition116 then
    begin
        ret = 1; return;
    end;
     
end;
 
// 時間: 09:05 to 10:20
// 出手時機: 開高續漲
// 1. value2 < -1800
// 2. value1 在10分鐘增加600
// 3. 34 68均線向上 and 34分線高於68分線
// 4. 指數離68分線30點之內 and 指數高於34分線
// 5. DIF 趨勢向上 and DIF 小於3.5
if todayTime >= 090500 and todayTime < 102000 then
begin
    condition101 = value2 < -1800;
    condition102 = (value1 - minlist(value1[1], value1[2], value1[3], value1[4], value1[5], value1[6], value1[7], value1[8], value1[9]) > 600);
    condition103 = (aveDir34 = 1) and (aveDir68 = 1) and (ave34 > ave68);
    condition104 = (close - ave68 < 30) and (close > ave34);
    condition105 = difValue < 3.5 and difDir = 1;
     
    print("策略10: ", condition101, condition102, condition103, condition104, condition105 );
    if condition101 and condition102 and condition103 and condition104 and condition105 then
    begin
        ret = 1; return;
    end;
     
end;

// 時間: 09:15 to 10:15
// 出手時機: 開平震盪走高
// 1. value2 < -700
// 2. value1 在10分鐘內增加1000以上
// 3. 四線向上
// 4. 均線3多頭排列 and 17 and 68 分線在15點之內
// 5. (DIF < 0.55 and DIF > 0) and DIF 向上
// 6. 指數為均線之上 and 離68分線30點內
if todayTime >= 091500 and todayTime < 101500 then
begin
 
    condition91 = value2 < -700;
    condition92 = (value1 - minlist(value1[1], value1[2], value1[3], value1[4], value1[5], value1[6], value1[7], value1[8], value1[9]) > 1000);
    condition93 = (aveDir8 = 1) and (aveDir17 = 1) and (aveDir34 = 1) and (aveDir68 = 1);
    condition94 = (ave17 > ave34) and (ave34 > ave68) and (ave17 - ave68 < 15);
    condition95 = ((difValue < 0.55) and (difValue > 0)) and (difDir = 1);
    condition96 = (close > ave8) and (close - ave68 < 30);
     
    print("策略09: ", condition91, condition92, condition93, condition94, condition95, condition96);
    if condition91 and condition92 and condition93 and condition94 and condition95 and condition96 then
    begin
        ret = 1; return;
    end;
     
end;
 
 
// 時間: 09:15 to 10:15
// 出手時機: 小多小步起漲
// 1. value2 < 750
// 2. 四線向上
// 3. 均線多頭排列
// 4. (DIF < 0.55 and DIF > 0) and DIF 向上
// 5. 四線17 68距離在3點之內 and 指數四線之上 and 指數離68分線8點之內
if todayTime >= 090400 and todayTime < 101500 then
begin
 
    condition81 = value2 < 750;
    condition82 = (aveDir8 = 1) and (aveDir17 = 1) and (aveDir34 = 1) and (aveDir68 = 1);
    condition83 = (ave8 > ave17) and (ave17 > ave34) and (ave34 > ave68) and (ave17 - ave68 < 30);
    condition84 = ((difValue < 0.55) and (difValue > 0)) and (difDir = 1);
    condition85 = (ave17 - ave68 < 3) and (close > ave8) and (close - ave68 < 8);
     
    print("策略08: ", condition81, condition82, condition83, condition84, condition85);
    if condition81 and condition82 and condition83 and condition84 and condition85 then
    begin
        ret = 1; return;
    end;
     
end;
 
// 時間: 09:04 to 10:15
// 出手時機: 散戶做空，開高走高盤中進場
// 1. value2 < -800
// 2. value1 在10分鐘內增加800
// 3. 17 34 68 均線趨勢向上
// 4. 17 34 68 均線多頭排列 and 17和68差距在30點之內
// 5. DIF 大於 0.5 and 指數大於ave34 and 指數離ave34 15點之內
// 6. 指數要上漲 GetQuote("當日漲幅%") > 0
// 7. DIF方向要向上
if todayTime >= 090400 and todayTime < 101500 then
begin
    condition71 = value2 < -800;
    condition72 = (value1 - minlist(value1[1], value1[2], value1[3], value1[4], value1[5], value1[6], value1[7], value1[8], value1[9]) > 800);
    condition73 = (aveDir17 = 1 and aveDir34= 1 and aveDir68 = 1);
    condition74 = (ave17 > ave34) and (ave34 > ave68) and (ave17 - ave68 < 30);
    condition75 = (difValue > 0.5) and (close > ave34) and (close - ave34 < 15);
	condition76 = GetQuote("當日漲幅%") > 0;
	condition77 = difDir = 1;
	
	// 如果 0915 分前 value2 特別要小於 -1500
	if todayTime <= 091500 then
		condition71 = value2 < -1500;
     
    print("策略07: ", condition71, condition72, condition73, condition74, condition75, condition76, condition77);
    if condition71 and condition72 and condition73 and condition74 and condition75 and condition76 and condition77 then
    begin
        ret = 1; return;
    end;
end;
 
// 時間: 09:04 to 09:54
// 出手時機: 均線糾結完成逐步震盪走高
// 1. value1 在10分鐘內增加300以上
// 2. 8 17 34 有連續2分鐘向上
// 3. DIF作多向上 and 0軸之間(大於-0.1 小於0.3)
// 4. 均線糾結(四線差小於6點內)
// 5. 均線糾結(指數離最低均線6點內)
if todayTime >= 090400 and todayTime < 095400 then
begin
    condition61 = (value1 - minlist(value1[1], value1[2], value1[3], value1[4], value1[5], value1[6], value1[7], value1[8], value1[9]) > 300);
    condition62 = (aveDir8 = 1 and aveDir17 = 1 and aveDir34 = 1) and (aveDir8[1] = 1 and aveDir17[1] = 1 and aveDir34[1] = 1);
    condition63 = (difDir = 1) and (difValue > -0.1 and difValue < 0.3);
    condition64 = AbsValue(maxlist(ave8, ave17, ave34, ave68) - minlist(ave8, ave17, ave34, ave68)) < 6;
    condition65 = AbsValue(close - minlist(ave8, ave17, ave34, ave68)) < 6;
 
    print("策略06: ", condition61, condition62, condition63, condition64, condition65);
    if condition61 and condition62 and condition63 and condition64 and condition65 then
    begin
        ret = 1; return;
    end;
end;
 
// 時間: 09:10 to 09:44
// 出手時機: 開高震盪趁再走高
// 1. value1 在10分鐘內增加1000以上
// 2. value2 在10分鐘內減少200
// 3. 四線向上 and 17 34 68多頭排列 and 指數站上8分線上
// 4. DIF作多 and DIF大於0
// 5. ave8 和 ave68 差距要小於22
// 6. vaule2 < -200
// 7. 如果指數上漲超過0.45 value2 需要 < -1000 (避免震盪盤)
if todayTime >= 091000 and todayTime < 094400 then
begin
    condition51 = (value1 - minlist(value1[1], value1[2], value1[3], value1[4], value1[5], value1[6], value1[7], value1[8], value1[9]) > 1000);
    condition52 = (maxlist(value2[1], value2[2], value2[3], value2[4], value2[5], value2[6], value2[7], value2[8], value2[9]) - value2 > 200);
    condition53 = (aveDir8 = 1 and aveDir17 = 1 and aveDir34 = 1 and aveDir68 = 1) and (ave17 > ave34) and (ave34 > ave68) and (close > ave8);
    condition54 = difDir = 1 and difValue > 0;
    condition55 = (ave8 - ave68 < 22);
	condition56 = value2 < -200;
	condition57 = (GetQuote("當日漲幅%") < 0.45) or (value2 < -1000);
     
    print("策略05: ", condition51, condition52, condition53, condition54, condition55, condition56, condition57);
    if condition51 and condition52 and condition53 and condition54 and condition55 and condition56 and condition57 then
    begin
        ret = 1; return;
    end;
end;
 
 
// 時間: 0904 to 10:15
// 出手時機: 累計委買空翻多，逐步走高同時均線走高 MACD作多
// 1. value1 在10分鐘內由空翻多 增加500
// 2. 8 17 34均線連續3分鐘向上
// 3. 四線多頭排列 and 指數站上8分線上
// 4. MACD 黃金交叉 and DIF作多
// 5. ave8 和 ave68 差距要大於1 and 小於8 (均線糾結剛散開)
if todayTime >= 090400 and todayTime < 101500 then
begin
    condition41 = (value1 - minlist(value1[1], value1[2], value1[3], value1[4], value1[5], value1[6], value1[7], value1[8], value1[9]) > 500);
    condition42 = (aveDir8 = 1 and aveDir17 = 1 and aveDir34 = 1) and (aveDir8[1] = 1 and aveDir17[1] = 1 and aveDir34[1] = 1) and (aveDir8[2] = 1 and aveDir17[2] = 1 and aveDir34[2] = 1);
    condition43 = (aveDir8 > aveDir17 and aveDir17 > aveDir34 and aveDir34 > aveDir68) and close > ave8;
    condition44 = (macdUnder = 1) and difDir = 1;
    condition45 = (ave8 - ave68 > 1) and (ave8 - ave68 < 8);
     
    print("策略04: ", condition41, condition42, condition43, condition44, condition45);
    if condition41 and condition42 and condition43 and condition44 and condition45 then
    begin
        ret = 1; return;
    end;
end;
 
// 時間: 0910 to 10:15
// 出手時機: 上升軌道急跌回到68分線進場繼續做多
// 1. MACD處黃金交叉 and DIF向上
// 2. 10分鐘內有出現 8分線 黃金交叉 68分線
// 3. 8分線向上 and 68分線連續50分鐘都是上升
// 4. value2 < -600
if todayTime >= 091000 and todayTime < 100500 then
begin
    condition31 = (macdUnder = 1) and difDir = 1;
    condition32 = isAveGoldenCrossInMinutes(8, 68, 10);
    condition33 = isAveRaiseOverMinutes(68, 50) and aveDir8 = 1;
	condition34 = value2 < -600;
     
    print("策略03: ", condition31, condition32, condition33, condition34);
    if condition31 and condition32 and condition33 and condition34 then
    begin
        ret = 1; return;
    end;
end;
 
 
// 時間: 0910 to 09:55
// 出手時機: 開高震盪再創高
// 1. value1 在5分鐘內不能減少超過200
// 2. DIF 比前一分鐘增加
// 3. 指數離34分線21點內
// 4. 三線向上(8分線怕短線急跌造成短線下彎) and 指數站上34分線上
// 5. value2 < -300
// 6. 如果指數上漲超過0.45 value2 需要 < -1000 (避免震盪盤)
if todayTime >= 091000 and todayTime < 095500 then
begin
    condition21 = maxlist(value1[1], value1[2], value1[3], value1[4], value1[5]) - value1 < 200;
    condition22 = (difValue - difValue[1] > 0);
    condition23 = (close - 21) < ave34;
    condition24 = (aveDir17 = 1 and aveDir34 = 1 and aveDir68 = 1) and close > ave34;
    condition25 = value2 < -300;
	condition26 = (GetQuote("當日漲幅%") < 0.45) or (value2 < -1000);
    
    print("策略02: ", condition21, condition22, condition23, condition24, condition25, condition26);
    if condition21 and condition22 and condition23 and condition24 and condition25 and condition26 then
    begin
        ret = 1; return;
    end;
end;
 
 
// 時間: 0903 to 09:55
// 出手時機: 開平盤逐漸起漲
// 1. value1 在10分鐘內成長500 and value2 < -300
// 2. 8 17 34 68 都向上
// 3. macd 黃金交叉
// 4. 股數離34分線 25 點之內 and 全均線之上
// 5. DIF 向上
// 6. 如果指數上漲超過0.45 value2 需要 < -1000 (避免震盪盤)
if todayTime >= 090300 and todayTime < 095500 then
begin
    condition11 = (value1 > minlist(value1[1], value1[2], value1[3], value1[4], value1[5], value1[6], value1[7], value1[8], value1[9])+500) and value2 < -300;
    condition12 = (aveDir8 = 1 and aveDir17 = 1 and aveDir34 = 1 and aveDir68 = 1);
    condition13 = (macdUnder = 1);
    condition14 = close > ave8 and close > ave17 and close > ave34 and close > ave68 and (close - 25) < ave34;
	condition15 = difDir = 1;
	condition16 = (GetQuote("當日漲幅%") < 0.45) or (value2 < -1000);
     
    print("策略01: ", condition11, condition12, condition13, condition14, condition15, condition16);
    if condition11 and condition12 and condition13 and condition14 and condition15 and condition16 then
    begin
        ret = 1; return;
    end;
end;