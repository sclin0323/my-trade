// 執行頻率需為 60 分鐘
if barinterval <> 60 then RaiseRunTimeError("請設定頻率為60分鐘");
variable: todayDate(0); todayDate = GetField("Date"); 
variable: todayTime(0); todayTime = GetField("Time");

// 計算均線  
variable: ave18(0); 	ave18 = Average(Close,18);			// 18小時均線	
variable: aveDir18(0);	aveDir18 = Sign(ave18 - ave18[1]);	// 18小時均線方向
  
// 計算MACD
input: FastLength(12), SlowLength(26), MACDLength(9);	// 依照XQ軟體預設值
variable: difValue(0), macdValue(0), oscValue(0);
MACD(weightedclose(), FastLength, SlowLength, MACDLength, difValue, macdValue, oscValue);
variable: macdDir(0); macdDir = Sign(macdValue - macdValue[2]);		// MACD 方向
variable: difDir(0); difDir = Sign(difValue - difValue[2]);			// DIF 方向 (向下 => -1)
variable: macdUnder(0); macdUnder = Sign(difValue - macdValue);		// DIF 和 MACD 的上下位置

//////////////////////////////////////////////////////////////////////////////////////////////////////

// 條件2: 長線趨勢如果-15分&DIF弱勢 => 不要做多, 留給別人賺!! (真的被洗怕了!!)
// 1. 長線趨勢-15分
// 2. difValue < -30 (弱勢)
// 3. 過去3K漲幅小於80點 (弱勢) (如果過去3K漲幅超過80點, 多頭啟動 => 該條件不成立)
// 4. 如果指數離18k只有30點~ 多頭啟動 => 該條件不成立 
condition21 = Summation(aveDir18,15) = -15;
condition22 = difValue < -30;
condition23 = close - lowest(low,3) < 80;
condition24 = close + 30 < ave18;
if condition21 and condition22 and condition23 and condition24 then
begin
	print("長線趨勢-15分&DIF弱勢 => 不要做多, 留給別人賺!! (真的被洗怕了!!)");
	return;
end;

///////////////////////////////////////////////////////////////////////////////////////////////////////

// 條件1: 符合以下其中一條件, 短多啟動
// 1. 60分鐘 DIF連續出現向上(1+2 or 1+3)
// 2. MACD向上(2hrs)
// 3. DIF < 5
condition11 = difDir = 1 and (difDir[1] = 1 or difDir[2] = 1);
condition12 = macdDir = 1 or macdDir[1] = 1;
condition13 = difDir < 5;

if condition11 or condition12 or condition13 then
begin
	print(DateToString(todayDate), TimeToString(todayTime),"參照條件符合:",condition11, condition12, condition13);
	ret = 1;
	return;
end;

print(DateToString(todayDate), TimeToString(todayTime), "參照條件不符合",condition11, condition12, condition13);
